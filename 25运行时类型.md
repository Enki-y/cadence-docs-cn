# 运行时类型

------

类型可以在运行时表示。要创建类型值，请使用构造函数`Type<T>()`，它接受静态类型作为类型参数。

这是类似的，例如`T.self`在Swift，`T::class`/`KClass<T>`在Kotlin，和`T.class`/ `Class<T>`Java中。

例如，要`Int`在运行时表示类型：

```cadence
let intType: Type = Type<Int>()
```

这适用于内置和用户定义的类型。例如，要获取资源的类型值：

```cadence
resource Collectible {}

let collectibleType = Type<@Collectible>()

// collectibleType` 具有 `Type`类型
```

类型值是可比较的。

```cadence
Type<Int>() == Type<Int>()

Type<Int>() != Type<String>()
```

要获取运行时类型的完全限定类型标识符，请使用以下`let identifier: String`字段：

```cadence
let type = Type<Int>()
type.identifier  // 是 "Int"
```

```cadence
// 在账户 0x1

struct Test {}

let type = Type<Test>()
type.identifier  // 是 "A.0000000000000001.Test"
```

### 从值中获取类型

该方法`fun getType(): Type`可用于获取值的运行时类型。

```cadence
let something = "hello"

let type: Type = something.getType()
// `type` 是 `Type<String>()`
```

此方法返回对象的**具体运行时类型**，**而不是**静态类型。

```cadence
// 声明一个名为 `something` 的变量，它具有 static 类型 `AnyResource`并且有一个 `Collectible` 类型的资源
//
let something: @AnyResource <- create Collectible()

// 资源的具体运行时类型是`Collectible`
//
let type: Type = something.getType()
// `type` 是 `Type<@Collectible>()`
```

### 断言值的类型

该方法`fun isInstance(_ type: Type): Bool`可用于检查值是否具有某种类型，使用具体的运行时类型，并考虑子类型规则，

```cadence
// 声明一个名为 `collectible` 的变量，它具有 static 类型 `Collectible`并且有一个 `Collectible` 类型的资源
//
let collectible: @Collectible <- create Collectible()

// 资源是一个 `Collectible` 类型的实例，因为具体的运行时类型是`Collectible`
//
collectible.isInstance(Type<@Collectible>())  // is `true`

// 资源是一个 `AnyResource` 类型的实例，因为具体的运行时类型 `Collectible` 是 `AnyResource` 的子类型
//
collectible.isInstance(Type<@AnyResource>())  // is `true`

// 资源不是类型为 `String` 的实例， 因为具体的运行时类型 `Collectible` 不是 `String` 的子类型
//
collectible.isInstance(Type<String>())  // is `false`
```

请注意，使用的是对象的**具体运行时类型**，**而不是**静态类型。

```cadence
// 声明一个名为 `something` 的变量，它具有 static类型 `AnyResource`并且有一个 `Collectible` 类型的资源
//
let something: @AnyResource <- create Collectible()

// 资源是一个 `Collectible` 类型的实例，因为具体的运行时类型是`Collectible`
//
something.isInstance(Type<@Collectible>())  // is `true`

// 资源是一个 `AnyResource` 类型的实例， 因为具体的运行时类型 `Collectible` 是 `AnyResource` 的子类型
//
something.isInstance(Type<@AnyResource>())  // is `true`

// 资源不是类型为 `String` 的实例， 因为具体的运行时类型 `Collectible`不是`String` 的子类型
something.isInstance(Type<String>())  // is `false`
```

例如，这允许实施市场销售资源：

```cadence
pub resource SimpleSale {

    /// 待售资源。资源售出后，该字段变为`nil`。
    ///
    pub var resourceForSale: @AnyResource?

    /// 购买资源所需的价格。
    ///
    pub let priceForResource: UFix64

    /// 购买所需的货币类型。
    ///
    pub let requiredCurrency: Type
    pub let paymentReceiver: Capability<&{FungibleToken.Receiver}>

    ///一旦进行了有效的购买， `paymentReceiver` 是将被借用的权限。预计目标是允许存入支付金额的资源（类型为`requiredCurrency`的保险库）。
    ///
    init(
        resourceForSale: @AnyResource,
        priceForResource: UFix64,
        requiredCurrency: Type,
        paymentReceiver: Capability<&{FungibleToken.Receiver}>
    ) {
        self.resourceForSale <- resourceForSale
        self.priceForResource = priceForResource
        self.requiredCurrency = requiredCurrency
        self.paymentReceiver = paymentReceiver
    }

    destroy() {
        // 当这个销售资源被销毁时，同时销毁要出售的资源。另一种选择是将其转回给卖家。
        destroy self.resourceForSale
    }

    /// buyObject 允许通过提供购买资源来出售所需的资金。如果购买成功，则返回待售资源。如果购买失败，程序中止。
    ///
    pub fun buyObject(with funds: @FungibleToken.Vault): @AnyResource {
        pre {
            // 确保资源仍在出售中
            self.resourceForSale != nil: "The resource has already been sold"
            // 确保支付的资金有正确的金额
            funds.balance >= self.priceForResource: "Payment has insufficient amount"
            // 确保支付的货币正确
            funds.isInstance(self.requiredCurrency): "Incorrect payment currency"
        }

        // 通过借用此销售资源的付款接收者权限将支付的资金转给收款人并将付款存入其中

        let receiver = self.paymentReceiver.borrow()
            ?? panic("failed to borrow payment receiver capability")

        receiver.deposit(from: <-funds)
        let resourceForSale <- self.resourceForSale <- nil
        return <-resourceForSale
    }
}
```



