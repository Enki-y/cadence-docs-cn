# 接口

------

接口是一种抽象类型，它指定*实现*接口的类型的行为。接口声明所需的函数和字段、这些声明的访问控制以及实现类型需要提供的前置条件和后置条件。

共有三种接口：

- **结构接口**：由[结构](https://docs.onflow.org/cadence/language/composite-types#structures)实现
- **资源接口**：由[资源](https://docs.onflow.org/cadence/language/composite-types#resources)实现
- **合约接口**：由[合约](https://docs.onflow.org/cadence/language/contracts)实现

结构、资源和契约类型可以实现多个接口。

不支持事件和枚举接口。

名义类型适用于实现接口的复合类型。这意味着一个类型只有在显式声明了一致性的情况下才实现接口，复合类型不会隐式地符合接口，即使它满足接口的所有要求。

接口由实现接口的类型必须为其提供实现的功能和字段要求组成。接口要求及其实现必须始终至少是公开的。

可以对可变字段要求进行注释以要求它们可公开设置。

函数要求包括函数名称、参数类型、可选的返回类型以及可选的前置条件和后置条件。

字段要求包括字段的名称和类型。字段需求可以选择声明一个 getter 需求和一个 setter 需求，每个需求都有前置条件和后置条件。

在接口上调用具有前置条件和后置条件而不是具体实现的函数可以提高程序的安全性，因为它确保即使实现发生变化，它们的某些方面也将始终保持不变。

## 接口声明

接口所使用的声明`struct`，`resource`或`contract`关键字，其次是`interface`关键字时，接口的名称，和的要求，其必须封装在开闭括号。

通过使用`var`关键字，可以注释字段要求以要求实现是可变字段；通过使用`let`关键字，要求实现是一个常量字段；或者字段要求可以不指定任何内容，在这种情况下，实现可以是可变字段、常量字段或合成字段。

字段要求和功能要求必须指定所需的访问级别。访问必须至少是公开的，因此`pub`必须提供关键字。可以使用`pub(set)`关键字将变量字段要求指定为也可以公开设置。

接口可以在类型中使用。这[在类型](https://docs.onflow.org/cadence/language/interfaces/#interfaces-in-types)中的[接口](https://docs.onflow.org/cadence/language/interfaces/#interfaces-in-types)部分中有详细解释。目前，语法`{I}`可以理解为实现 interface 的任何值的类型`I`。

```cadence
// Declare a resource interface for a fungible token.
// Only resources can implement this resource interface.
//
pub resource interface FungibleToken {

    // Require the implementing type to provide a field for the balance
    // that is readable in all scopes (`pub`).
    //
    // Neither the `var` keyword, nor the `let` keyword is used,
    // so the field may be implemented as either a variable field,
    // a constant field, or a synthetic field.
    //
    // The read balance must always be positive.
    //
    // NOTE: no requirement is made for the kind of field,
    // it can be either variable or constant in the implementation.
    //
    pub balance: Int {
        set(newBalance) {
            pre {
                newBalance >= 0:
                    "Balances are always set as non-negative numbers"
            }
        }
    }

    // Require the implementing type to provide an initializer that
    // given the initial balance, must initialize the balance field.
    //
    init(balance: Int) {
        pre {
            balance >= 0:
                "Balances are always non-negative"
        }
        post {
            self.balance == balance:
                "the balance must be initialized to the initial balance"
        }

        // NOTE: The declaration contains no implementation code.
    }

    // Require the implementing type to provide a function that is
    // callable in all scopes, which withdraws an amount from
    // this fungible token and returns the withdrawn amount as
    // a new fungible token.
    //
    // The given amount must be positive and the function implementation
    // must add the amount to the balance.
    //
    // The function must return a new fungible token.
    // The type `{FungibleToken}` is the type of any resource
    // that implements the resource interface `FungibleToken`.
    //
    pub fun withdraw(amount: Int): @{FungibleToken} {
        pre {
            amount > 0:
                "the amount must be positive"
            amount <= self.balance:
                "insufficient funds: the amount must be smaller or equal to the balance"
        }
        post {
            self.balance == before(self.balance) - amount:
                "the amount must be deducted from the balance"
        }

        // NOTE: The declaration contains no implementation code.
    }

    // Require the implementing type to provide a function that is
    // callable in all scopes, which deposits a fungible token
    // into this fungible token.
    //
    // No precondition is required to check the given token's balance
    // is positive, as this condition is already ensured by
    // the field requirement.
    //
    // The parameter type `{FungibleToken}` is the type of any resource
    // that implements the resource interface `FungibleToken`.
    //
    pub fun deposit(_ token: @{FungibleToken}) {
        post {
            self.balance == before(self.balance) + token.balance:
                "the amount must be added to the balance"
        }

        // NOTE: The declaration contains no implementation code.
    }
}
```

请注意，所需的初始化程序和函数没有任何可执行代码。

结构和资源接口只能直接在契约内部声明，即不能在函数内部声明。合约接口只能全局声明，不能在合约内部声明。

## 接口实现

声明类型实现（符合）接口是在复合类型（例如，结构、资源）的类型声明中完成的：复合类型的种类和名称后跟冒号 ( `:`) 和一个的名称或复合类型实现的更多接口。

这将告诉检查器将来自指定接口的任何要求强制执行到声明的类型上。

一个类型实现（符合）一个接口，如果它在其签名中声明实现，为接口所需的所有字段提供字段声明，并为接口所需的所有功能提供实现。

实现类型中的字段声明必须在名称、类型和声明类型（例如常量、变量）方面与接口中的字段要求相匹配（如果给定）。例如，接口可能需要具有特定名称和类型的字段，但将字段的类型留给实现。

函数实现必须在名称、参数参数标签、参数类型和返回类型方面与接口中的函数要求相匹配。

```cadence
// Declare a resource named `ExampleToken` that has to implement
// the `FungibleToken` interface.
//
// It has a variable field named `balance`, that can be written
// by functions of the type, but outer scopes can only read it.
//
pub resource ExampleToken: FungibleToken {

    // Implement the required field `balance` for the `FungibleToken` interface.
    // The interface does not specify if the field must be variable, constant,
    // so in order for this type (`ExampleToken`) to be able to write to the field,
    // but limit outer scopes to only read from the field, it is declared variable,
    // and only has public access (non-settable).
    //
    pub var balance: Int

    // Implement the required initializer for the `FungibleToken` interface:
    // accept an initial balance and initialize the `balance` field.
    //
    // This implementation satisfies the required postcondition.
    //
    // NOTE: the postcondition declared in the interface
    // does not have to be repeated here in the implementation.
    //
    init(balance: Int) {
        self.balance = balance
    }

    // Implement the required function named `withdraw` of the interface
    // `FungibleToken`, that withdraws an amount from the token's balance.
    //
    // The function must be public.
    //
    // This implementation satisfies the required postcondition.
    //
    // NOTE: neither the precondition nor the postcondition declared
    // in the interface have to be repeated here in the implementation.
    //
    pub fun withdraw(amount: Int): @ExampleToken {
        self.balance = self.balance - amount
        return create ExampleToken(balance: amount)
    }

    // Implement the required function named `deposit` of the interface
    // `FungibleToken`, that deposits the amount from the given token
    // to this token.
    //
    // The function must be public.
    //
    // NOTE: the type of the parameter is `{FungibleToken}`,
    // i.e., any resource that implements the resource interface `FungibleToken`,
    // so any other token – however, we want to ensure that only tokens
    // of the same type can be deposited.
    //
    // This implementation satisfies the required postconditions.
    //
    // NOTE: neither the precondition nor the postcondition declared
    // in the interface have to be repeated here in the implementation.
    //
    pub fun deposit(_ token: @{FungibleToken}) {
        if let exampleToken <- token as? ExampleToken {
            self.balance = self.balance + exampleToken.balance
            destroy exampleToken
        } else {
            panic("cannot deposit token which is not an example token")
        }
    }
}

// Declare a constant which has type `ExampleToken`,
// and is initialized with such an example token.
//
let token <- create ExampleToken(balance: 100)

// Withdraw 10 units from the token.
//
// The amount satisfies the precondition of the `withdraw` function
// in the `FungibleToken` interface.
//
// Invoking a function of a resource does not destroy the resource,
// so the resource `token` is still valid after the call of `withdraw`.
//
let withdrawn <- token.withdraw(amount: 10)

// The postcondition of the `withdraw` function in the `FungibleToken`
// interface ensured the balance field of the token was updated properly.
//
// `token.balance` is `90`
// `withdrawn.balance` is `10`

// Deposit the withdrawn token into another one.
let receiver: @ExampleToken <- // ...
receiver.deposit(<-withdrawn)

// Run-time error: The precondition of function `withdraw` in interface
// `FungibleToken` fails, the program aborts: the parameter `amount`
// is larger than the field `balance` (100 > 90).
//
token.withdraw(amount: 100)

// Withdrawing tokens so that the balance is zero does not destroy the resource.
// The resource has to be destroyed explicitly.
//
token.withdraw(amount: 90)
```

实现中变量字段的访问级别可能比接口要求的限制更少。例如，一个接口可能要求一个字段至少是公开的（即`pub`指定关键字），而一个实现可以提供一个公共的变量字段，但也可以公开设置（`pub(set)`指定关键字）。

```cadence
pub struct interface AnInterface {
    // Require the implementing type to provide a publicly readable
    // field named `a` that has type `Int`. It may be a constant field,
    // a variable field, or a synthetic field.
    //
    pub a: Int
}

pub struct AnImplementation: AnInterface {
    // Declare a publicly settable variable field named `a` that has type `Int`.
    // This implementation satisfies the requirement for interface `AnInterface`:
    // The field is at least publicly readable, but this implementation also
    // allows the field to be written to in all scopes.
    //
    pub(set) var a: Int

    init(a: Int) {
        self.a = a
    }
}
```

## 类型中的接口

接口可以在类型中使用：类型`{I}`是实现接口的所有对象的类型`I`。

这称为[受限类型](https://docs.onflow.org/cadence/language/restricted-types)：访问此类类型的值时，只能使用接口的功能（成员和函数）。

```cadence
// Declare an interface named `Shape`.
//
// Require implementing types to provide a field which returns the area,
// and a function which scales the shape by a given factor.
//
pub struct interface Shape {
    pub fun getArea(): Int
    pub fun scale(factor: Int)
}

// Declare a structure named `Square` the implements the `Shape` interface.
//
pub struct Square: Shape {
    // In addition to the required fields from the interface,
    // the type can also declare additional fields.
    //
    pub var length: Int

    // Provided the field `area`  which is required to conform
    // to the interface `Shape`.
    //
    // Since `area` was not declared as a constant, variable,
    // field in the interface, it can be declared.
    //
    pub fun getArea(): Int {
        return self.length * self.length
    }

    pub init(length: Int) {
        self.length = length
    }

    // Provided the implementation of the function `scale`
    // which is required to conform to the interface `Shape`.
    //
    pub fun scale(factor: Int) {
        self.length = self.length * factor
    }
}

// Declare a structure named `Rectangle` that also implements the `Shape` interface.
//
pub struct Rectangle: Shape {
    pub var width: Int
    pub var height: Int

    // Provided the field `area  which is required to conform
    // to the interface `Shape`.
    //
    pub fun getArea(): Int {
        return self.width * self.height
    }

    pub init(width: Int, height: Int) {
        self.width = width
        self.height = height
    }

    // Provided the implementation of the function `scale`
    // which is required to conform to the interface `Shape`.
    //
    pub fun scale(factor: Int) {
        self.width = self.width * factor
        self.height = self.height * factor
    }
}

// Declare a constant that has type `Shape`, which has a value that has type `Rectangle`.
//
var shape: {Shape} = Rectangle(width: 10, height: 20)
```

实现接口的值可分配给以接口作为其类型的变量。

```cadence
// Assign a value of type `Square` to the variable `shape` that has type `Shape`.
//
shape = Square(length: 30)

// Invalid: cannot initialize a constant that has type `Rectangle`.
// with a value that has type `Square`.
//
let rectangle: Rectangle = Square(length: 10)
```

可以访问接口中声明的字段，并且可以对实现该接口的类型的值调用接口中声明的函数。

```cadence
// Declare a constant which has the type `Shape`.
// and is initialized with a value that has type `Rectangle`.
//
let shape: {Shape} = Rectangle(width: 2, height: 3)

// Access the field `area` declared in the interface `Shape`.
//
shape.area  // is `6`

// Call the function `scale` declared in the interface `Shape`.
//
shape.scale(factor: 3)

shape.area  // is `54`
```

## 接口实现要求

接口可能需要实现类型来实现其他同类接口。接口实现需求可以通过在接口名称后加冒号（`:`）和一个或多个同类接口名称来声明，用逗号隔开。

```cadence
// Declare a structure interface named `Shape`.
//
pub struct interface Shape {}

// Declare a structure interface named `Polygon`.
// Require implementing types to also implement the structure interface `Shape`.
//
pub struct interface Polygon: Shape {}

// Declare a structure named `Hexagon` that implements the `Polygon` interface.
// This also is required to implement the `Shape` interface,
// because the `Polygon` interface requires it.
//
pub struct Hexagon: Polygon {}
```

## 接口嵌套

🚧 状态：目前只有合约和合约接口支持嵌套接口。

接口可以任意嵌套。在另一个内部声明一个接口不需要实现外部接口的类型来提供内部接口的实现。

```cadence
// Declare a resource interface `OuterInterface`, which declares
// a nested structure interface named `InnerInterface`.
//
// Resources implementing `OuterInterface` do not need to provide
// an implementation of `InnerInterface`.
//
// Structures may just implement `InnerInterface`.
//
resource interface OuterInterface {

    struct interface InnerInterface {}
}

// Declare a resource named `SomeOuter` that implements the interface `OuterInterface`
//
// The resource is not required to implement `OuterInterface.InnerInterface`.
//
resource SomeOuter: OuterInterface {}

// Declare a structure named `SomeInner` that implements `InnerInterface`,
// which is nested in interface `OuterInterface`.
//
struct SomeInner: OuterInterface.InnerInterface {}
```

## 嵌套类型要求

🚧 状态：目前只有契约和契约接口支持嵌套类型需求。

接口可能需要实现类型以提供具体的嵌套类型。例如，资源接口可能需要一个实现类型来提供资源类型。

```cadence
// Declare a resource interface named `FungibleToken`.
//
// Require implementing types to provide a resource type named `Vault`
// which must have a field named `balance`.
//
resource interface FungibleToken {

    pub resource Vault {
        pub balance: Int
    }
}

// Declare a resource named `ExampleToken` that implements the `FungibleToken` interface.
//
// The nested type `Vault` must be provided to conform to the interface.
//
resource ExampleToken: FungibleToken {

    pub resource Vault {
        pub var balance: Int

        init(balance: Int) {
            self.balance = balance
        }
    }
}
```

## `Equatable` 界面

🚧 状态：`Equatable`接口尚未实现。

相等类型是可以比较相等性的类型。类型在实现`Equatable`接口时是相等的。

可以使用等于运算符 ( `==`)比较可相等类型的相等性或使用不等于运算符 ( ) 比较不等式`!=`。

大多数内置类型都是等价的，如布尔值和整数。当数组的元素可相等时，数组是可相等的。当字典的值相等时，字典是相等的。

为了使类型相等，`Equatable`必须实现接口，这需要实现 function `equals`，它接受另一个值，应该比较给定的值是否相等。

```cadence
struct interface Equatable {
    pub fun equals(_ other: {Equatable}): Bool
}
```

```cadence
// Declare a struct named `Cat`, which has one field named `id`
// that has type `Int`, i.e., the identifier of the cat.
//
// `Cat` also will implement the interface `Equatable`
// to allow cats to be compared for equality.
//
struct Cat: Equatable {
    pub let id: Int

    init(id: Int) {
        self.id = id
    }

    pub fun equals(_ other: {Equatable}): Bool {
        if let otherCat = other as? Cat {
            // Cats are equal if their identifier matches.
            //
            return otherCat.id == self.id
        } else {
            return false
        }
    }
}

Cat(1) == Cat(2)  // is `false`
Cat(3) == Cat(3)  // is `true`
```

## `Hashable` 界面

🚧 状态：`Hashable`接口尚未实现。

可散列类型是可以散列为整数散列值的类型，即，将其提炼为用作不等式证据的值。类型在实现`Hashable`接口时是可散列的。

可散列类型可以用作字典中的键。

可散列类型也必须是可等同的，即它们还必须实现`Equatable`接口。这是因为哈希值只是不等式的证据：具有不同哈希值的两个值保证不相等。但是，如果两个值的哈希值相同，那么这两个值仍然可能不相等，并且恰好散列到相同的哈希值。在这种情况下，仍然需要通过相等性检查来确定相等性。如果没有`Equatable`，可以将值添加到字典中，但无法检索它们。

大多数内置类型都是可散列的，如布尔值和整数。当数组的元素是可散列的时，数组是可散列的。当字典的值相等时，字典是可散列的。

散列值意味着将其基本组件传递到散列函数中。基本组件是在`Equatable`.

如果两个值相等，因为它们的`equals`函数返回 true，那么实现必须为这两个值中的每一个返回相同的整数哈希值。

当基本组件没有改变时，实现还必须在程序执行期间一致地返回相同的整数哈希值。整数哈希值不必在多次执行中都相同。

```cadence
struct interface Hashable: Equatable {
    pub hashValue: Int
}
```



```cadence
// Declare a structure named `Point` with two fields
// named `x` and `y` that have type `Int`.
//
// `Point` is declared to implement the `Hashable` interface,
// which also means it needs to implement the `Equatable` interface.
//
struct Point: Hashable {

    pub(set) var x: Int
    pub(set) var y: Int

    init(x: Int, y: Int) {
        self.x = x
        self.y = y
    }

    // Implementing the function `equals` will allow points to be compared
    // for equality and satisfies the `Equatable` interface.
    //
    pub fun equals(_ other: {Equatable}): Bool {
        if let otherPoint = other as? Point {
            // Points are equal if their coordinates match.
            //
            // The essential components are therefore the fields `x` and `y`,
            // which must be used in the implementation of the field requirement
            // `hashValue` of the `Hashable` interface.
            //
            return otherPoint.x == self.x
                && otherPoint.y == self.y
        } else {
            return false
        }
    }

    // Providing an implementation for the hash value field
    // satisfies the `Hashable` interface.
    //
    pub synthetic hashValue: Int {
        get {
            // Calculate a hash value based on the essential components,
            // the fields `x` and `y`.
            //
            var hash = 7
            hash = 31 * hash + self.x
            hash = 31 * hash + self.y
            return hash
        }
    }
}
```

[
](https://docs.onflow.org/cadence/language/access-control/)