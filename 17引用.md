# 引用

可以创建对对象的引用，即资源或结构。引用可用于访问被引用对象上的字段和调用函数。
引用被复制，即它们是值类型。
引用是通过使用 & 运算符创建的，后跟引用对象、as 关键字和引用对象的类型。引用对象的类型必须是引用对象类型的父类型。
引用的类型为 &T，其中 T 是被引用对象的类型。

```
let hello = "Hello"

// 创建对"Hello"的引用
//
let helloRef: &String = &hello as &String

helloRef.length // is `5`

// 无效: 无法创建对"Hello"的引用，"Hello"类型是String，而不是Int
//
let intRef: &Int = &hello as &Int
```

引用的基类型是协变的。例如，&T 是 &U 的子类型，如果 T 是 U 的子类型。

```
// 申明一个资源接口 `HasCount`,它有一个只能读的资源`count`
//
resource interface HasCount {
    pub let count: Int
}

// 申明一个资源接口 `Counter`,它有一可修改的资源`count`，并且继承`HasCount`
//
pub resource Counter: HasCount {
    pub var count: Int

    init(count: Int) {
        self.count = count
    }

    pub fun increment() {
        self.count = self.count + 1
    }
}

// 创建一个实例`Counter`，并创建 `&Counter`引用该实例counter,因此`&Counter`可以访问counter的所有资源和方法
//
let counter <- create Counter(count: 42)
let counterRef: &Counter = &counter as &Counter

counterRef.count  // is `42`

counterRef.increment()

counterRef.count  // is `43`
```

引用可能是授权的或未经授权的。
授权引用有 auth 修饰符，即完整的语法是 auth &T，而未授权引用没有修饰符。
授权的引用可以自由地向上转换和向下转换，而未经授权的引用只能向上转换。此外，授权引用是未授权引用的子类型。

```
//创建对counter的未经授权的引用，使用受限类型 `&{HasCount}` 输入，也就是一些符合 `HasCount` 接口的资源
//
let countRef: &{HasCount} = &counter as &{HasCount}

countRef.count  // is `43`

// 无效：因为HasCount不能访问increment
//
countRef.increment()

// Invalid: Cannot conditionally downcast to reference type `&Counter`,
// as the reference `countRef` is unauthorized.
//
// The counter value has type `Counter`, which is a subtype of `{HasCount}`,
// but as the reference is unauthorized, the cast is not allowed.
// It is not possible to "look under the covers"
//无效：无法有条件地向下转换为引用类型 `&Counter`，
// 因为引用 `countRef` 是未经授权的。
//
// counter的类型为 `Counter`，它是 `{HasCount}` 的子类型，但由于引用未经授权，因此不允许强制转换。
//
let counterRef2: &Counter = countRef as? &Counter


//创建对计数器的授权引用，再次使用受限类型`{HasCount}`，即符合 `HasCount` 接口的一些资源
//
let authCountRef: auth &{HasCount} = &counter as auth &{HasCount}


// 有条件地向下转换为引用类型 `&Counter`。
// 这是有效的，因为引用 `authCountRef` 被授权
//
let counterRef3: &Counter = authCountRef as? &Counter

counterRef3.count  // is `43`

counterRef3.increment()

counterRef3.count  // is `44`
```

引用是短暂的，即它们不能被存储。相反，考虑存储能力并在需要时借用它。