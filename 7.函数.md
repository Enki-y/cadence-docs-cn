# 函数

函数是执行特定任务的语句序列。函数具有参数（输入）和可选的返回值（输出）。函数是类型化的：函数类型由参数类型和返回类型组成。

函数是值，即它们可以分配给常量和变量，并且可以作为参数传递给其他函数。这种行为通常被称为“first-class函数”。

## 函数声明

可以使用`fun`关键字声明函数，后跟声明名称、参数、可选返回类型以及调用函数时应执行的代码。

参数需要括在括号中。返回类型（如果有）与参数之间用冒号 ( `:`)分隔。函数代码需要用左括号和右括号括起来。

每个参数都必须有一个名称，即参数值在函数内可用的名称。

可以提供附加参数标签以要求函数调用使用标签为参数提供参数值。

参数标签使代码更加明确和可读。例如，当有多个相同类型的参数时，它们可以避免混淆参数的顺序。

参数标签应该命名，以便从函数调用的角度来看它们是有意义的。

参数标签位于参数名称之前。特殊参数标签`_`表示函数调用可以省略参数标签。如果函数声明中没有声明参数标签，则参数名称为函数声明的参数标签，函数调用必须使用参数名称作为参数标签。

每个参数都需要有一个类型注释，它跟在冒号后的参数名称之后。

函数调用可以为作为参数类型子类型的参数提供参数。

有**没有**可选参数，参数即默认值，及可变参数的功能，即利用参数任意金额即功能的支持。

```cadence
// Declare a function named `double`, which multiples a number by two.
//
// The special argument label _ is specified for the parameter,
// so no argument label has to be provided in a function call.
//
fun double(_ x: Int): Int {
    return x * 2
}

// Call the function named `double` with the value 4 for the first parameter.
//
// The argument label can be omitted in the function call as the declaration
// specifies the special argument label _ for the parameter.
//
double(2)  // is `4`
```

某些参数可能需要参数标签，而其他参数不需要参数标签。

```cadence
// Declare a function named `clamp`. The function takes an integer value,
// the lower limit, and the upper limit. It returns an integer between
// the lower and upper limit.
//
// For the first parameter the special argument label _ is used,
// so no argument label has to be given for it in a function call.
//
// For the second and third parameter no argument label is given,
// so the parameter names are the argument labels, i.e., the parameter names
// have to be given as argument labels in a function call.
//
fun clamp(_ value: Int, min: Int, max: Int): Int {
    if value > max {
        return max
    }

    if value < min {
        return min
    }

    return value
}

// Declare a constant which has the result of a call to the function
// named `clamp` as its initial value.
//
// For the first argument no label is given, as it is not required by
// the function declaration (the special argument label `_` is specified).
//
// For the second and this argument the labels must be provided,
// as the function declaration does not specify the special argument label `_`
// for these two parameters.
//
// As the function declaration also does not specify argument labels
// for these parameters, the parameter names must be used as argument labels.
//
let clamped = clamp(123, min: 0, max: 100)
// `clamped` is `100`
```



```cadence
// Declare a function named `send`, which transfers an amount
// from one account to another.
//
// The implementation is omitted for brevity.
//
// The first two parameters of the function have the same type, so there is
// a potential that a function call accidentally provides arguments in
// the wrong order.
//
// While the parameter names `senderAddress` and `receiverAddress`
// are descriptive inside the function, they might be too verbose
// to require them as argument labels in function calls.
//
// For this reason the shorter argument labels `from` and `to` are specified,
// which still convey the meaning of the two parameters without being overly
// verbose.
//
// The name of the third parameter, `amount`, is both meaningful inside
// the function and also in a function call, so no argument label is given,
// and the parameter name is required as the argument label in a function call.
//
fun send(from senderAddress: Address, to receivingAddress: Address, amount: Int) {
    // The function code is omitted for brevity.
    // ...
}

// Declare a constant which refers to the sending account's address.
//
// The initial value is omitted for brevity.
//
let sender: Address = // ...

// Declare a constant which refers to the receiving account's address.
//
// The initial value is omitted for brevity.
//
let receiver: Address = // ...

// Call the function named `send`.
//
// The function declaration requires argument labels for all parameters,
// so they need to be provided in the function call.
//
// This avoids ambiguity. For example, in some languages (like C) it is
// a convention to order the parameters so that the receiver occurs first,
// followed by the sender. In other languages, it is common to have
// the sender be the first parameter, followed by the receiver.
//
// Here, the order is clear – send an amount from an account to another account.
//
send(from: sender, to: receiver, amount: 100)
```

函数调用中参数的顺序必须与函数声明中参数的顺序匹配。

```cadence
// Declare a function named `test`, which accepts two parameters, named `first` and `second`
//
fun test(first: Int, second: Int) {
    // ...
}

// Invalid: the arguments are provided in the wrong order,
// even though the argument labels are provided correctly.
//
test(second: 1, first: 2)
```

函数可以嵌套，即一个函数的代码可以声明更多的函数。

```cadence
// Declare a function which multiplies a number by two, and adds one.
//
fun doubleAndAddOne(_ x: Int): Int {

    // Declare a nested function which multiplies a number by two.
    //
    fun double(_ x: Int) {
        return x * 2
    }

    return double(x) + 1
}

doubleAndAddOne(2)  // is `5`
```

## 函数重载

🚧 状态：未实现函数重载。

可以声明具有相同名称的函数，只要它们具有不同的参数标签集。这称为函数重载。

```cadence
// Declare a function named "assert" which requires a test value
// and a message argument.
//
fun assert(_ test: Bool, message: String) {
    // ...
}

// Declare a function named "assert" which only requires a test value.
// The function calls the `assert` function declared above.
//
fun assert(_ test: Bool) {
    assert(test, message: "test is false")
}
```

## 函数表达式

函数也可以用作表达式。语法与函数声明相同，只是函数表达式没有名称，即它们是匿名的。

```cadence
// Declare a constant named `double`, which has a function as its value.
//
// The function multiplies a number by two when it is called.
//
// This function's type is `((Int): Int)`.
//
let double =
    fun (_ x: Int): Int {
        return x * 2
    }
```

## 函数调用

可以调用（调用）函数。函数调用需要提供与函数有参数一样多的参数值。

```cadence
fun double(_ x: Int): Int {
    return x * 2
}

// Valid: the correct amount of arguments is provided.
//
double(2)  // is `4`

// Invalid: too many arguments are provided.
//
double(2, 3)

// Invalid: too few arguments are provided.
//
double()
```

## 函数类型

函数类型由函数的参数类型和函数的返回类型组成。

参数类型需要用括号括起来，后跟冒号 ( `:`)，并以返回类型结束。整个函数类型需要用括号括起来。first-class

```cadence
// Declare a function named `add`, with the function type `((Int, Int): Int)`.
//
fun add(a: Int, b: Int): Int {
    return a + b
}
```



```cadence
// Declare a constant named `add`, with the function type `((Int, Int): Int)`
//
let add: ((Int, Int): Int) =
    fun (a: Int, b: Int): Int {
        return a + b
    }
```

如果函数没有返回类型，则它隐式具有返回类型`Void`。



```cadence
// Declare a constant named `doNothing`, which is a function
// that takes no parameters and returns nothing.
//
let doNothing: ((): Void) =
    fun () {}
```

括号也控制优先级。例如，函数类型`((Int): ((): Int))`是一个函数的类型，它接受一个带有 type 的参数`Int`，并返回另一个函数，该函数不接受任何参数并返回一个`Int`。

type`[((Int): Int); 2]`指定了两个函数的数组类型，它们接受一个整数并返回一个整数。

参数标签不是函数类型的一部分。这样做的好处是，只要参数类型和返回类型匹配，具有不同参数标签的函数（可能由不同作者编写）是兼容的。它的缺点是对普通函数值的函数调用不能接受参数标签。



```cadence
// Declare a function which takes one argument that has type `Int`.
// The function has type `((Int): Void)`.
//
fun foo1(x: Int) {}

// Call function `foo1`. This requires an argument label.
foo1(x: 1)

// Declare another function which takes one argument that has type `Int`.
// The function also has type `((Int): Void)`.
//
fun foo2(y: Int) {}

// Call function `foo2`. This requires an argument label.
foo2(y: 2)

// Declare a variable which has type `((Int): Void)` and use `foo1`
// as its initial value.
//
var someFoo: ((Int): Void) = foo1

// Call the function assigned to variable `someFoo`.
// This is valid as the function types match.
// This does neither require nor allow argument labels.
//
someFoo(3)

// Assign function `foo2` to variable `someFoo`.
// This is valid as the function types match.
//
someFoo = foo2

// Call the function assigned to variable `someFoo`.
// This does neither require nor allow argument labels.
//
someFoo(4)
```

## 闭包

函数可以引用定义它的外部作用域的变量和常量。它被称为闭包，因为它关闭了那些变量和常量。闭包可以从变量和常量中读取并分配给它所引用的变量。



```cadence
// Declare a function named `makeCounter` which returns a function that
// each time when called, returns the next integer, starting at 1.
//
fun makeCounter(): ((): Int) {
    var count = 0
    return fun (): Int {
        // NOTE: read from and assign to the non-local variable
        // `count`, which is declared in the outer function.
        //
        count = count + 1
        return count
    }
}

let test = makeCounter()
test()  // is `1`
test()  // is `2`
```

## 参数传递行为

当参数传递给函数时，它们被复制。因此，当函数返回时，传递给函数的值在调用者的作用域中是不变的。这种行为被称为 [call-by-value](https://en.wikipedia.org/w/index.php?title=Evaluation_strategy&oldid=896280571#Call_by_value)。



```cadence
// Declare a function that changes the first two elements
// of an array of integers.
//
fun change(_ numbers: [Int]) {
    // Change the elements of the passed in array.
    // The changes are only local, as the array was copied.
    //
    numbers[0] = 1
    numbers[1] = 2
    // `numbers` is `[1, 2]`
}

let numbers = [0, 1]

change(numbers)
// `numbers` is still `[0, 1]`
```

参数是常数，即不允许分配给它们。



```cadence
fun test(x: Int) {
    // Invalid: cannot assign to a parameter (constant)
    //
    x = 2
}
```

## 函数前置条件和后置条件

函数可能有前置条件，也可能有后置条件。前置条件和后置条件可用于限制函数的输入（参数值）和输出（返回值）。

在执行函数之前，前提条件必须为真。前置条件是函数的一部分，由`pre`关键字引入，后跟条件块。

后置条件必须在函数执行后立即为真。后置条件是函数的一部分，由`post`关键字引入，后跟条件块。后置条件可能只出现在前置条件之后（如果有的话）。

条件块由一个或多个条件组成。条件是评估为布尔值的表达式。它们不能调用函数，即它们不能有副作用并且必须是纯表达式。此外，条件可能不包含函数表达式。

条件可以写在不同的行上，或者多个条件可以写在同一行上，用分号分隔。此语法遵循[statements](https://docs.onflow.org/cadence/language/syntax#semicolons)的语法。

在每个条件之后，可以在冒号后提供可选的描述。当条件失败时，条件描述用作错误消息。

在后置条件中，特殊常量`result`是指函数的结果。



```cadence
fun factorial(_ n: Int): Int {
    pre {
        // Require the parameter `n` to be greater than or equal to zero.
        //
        n >= 0:
            "factorial is only defined for integers greater than or equal to zero"
    }
    post {
        // Ensure the result will be greater than or equal to 1.
        //
        result >= 1:
            "the result must be greater than or equal to 1"
    }

    if n < 1 {
       return 1
    }

    return n * factorial(n - 1)
}

factorial(5)  // is `120`

// Run-time error: The given argument does not satisfy
// the precondition `n >= 0` of the function, the program aborts.
//
factorial(-2)
```

在后置条件中，特殊函数`before`可用于在调用函数之前获取表达式的值。



```cadence
var n = 0

fun incrementN() {
    post {
        // Require the new value of `n` to be the old value of `n`, plus one.
        //
        n == before(n) + 1:
            "n must be incremented by 1"
    }

    n = n + 1
}
```

[
](https://docs.onflow.org/cadence/language/operators/)