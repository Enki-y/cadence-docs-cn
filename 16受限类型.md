# 受限类型

​        结构体和资源类型可以被**限制**。限制是接口。受限类型只允许访问受限类型的成员和函数的子集，由限制指示。
受限类型的语法是 T{U1, U2, ... Un}，其中 T 是受限类型，具体的资源或结构类型，类型 U1 到 Un 是T需要遵循的限制、接口。
只有限制集中成员和函数的并集可用。
​        受限类型对于增加仅适用于该类型子集的函数的安全性很有用。例如，通过对参数类型使用受限类型，函数只能访问限制的功能：如果函数意外尝试访问其他功能，静态检查器会阻止这种情况。

```
// 申明一个资源接口 `HasCount`,它有一个只能读的资源`count`
//
resource interface HasCount {
    pub let count: Int
}

// 申明一个资源接口 `Counter`,它有一可修改的资源`count`，并且继承`HasCount`
//
pub resource Counter: HasCount {
    pub var count: Int

    init(count: Int) {
        self.count = count
    }

    pub fun increment() {
        self.count = self.count + 1
    }
}

// 创建一个实例`Counter`
let counter: @Counter <- create Counter(count: 42)

counter.count  //  `42`

counter.increment()

counter.count  //  `43`

// 将变量 `counter` 中的资源移动到一个新变量 `restrictedCounter`,
// 但使用受限类型“Counter{HasCount}”输入：
// 该变量可以保存任何 `Counter`，但只能保存功能
// 在给定的限制中定义，接口`HasCount`，可以被访问
let restrictedCounter: @Counter{HasCount} <- counter

// 无效：只有限制 `Count` 的功能可用，
// 并集中有只读字段 `count`，而不是 `Counter` 的函数 `increment’
//
restrictedCounter.increment()


// 将变量 `restrictedCounter` 中的资源移动到新变量 `unrestrictedCounter`，
// 再次输入为 `Counter`，即计数器的所有功能都可用
//
let unrestrictedCounter: @Counter <- restrictedCounter


// 有效：变量 `unrestrictedCounter` 的类型为 `Counter`，
// 所以它的所有功能都可用，包括函数`increment`
//
unrestrictedCounter.increment()

// 声明另一个名为 `Strings` 的资源类型
// 实现了资源接口`HasCount`
//
pub resource Strings: HasCount {
    pub var count: Int
    access(self) var strings: [String]

    init() {
        self.count = 0
        self.strings = []
    }

    pub fun append(_ string: String) {
        self.strings.append(string)
        self.count = self.count + 1
    }
}


// 无效：资源类型 `Strings` 与受限类型 `Counter{HasCount}`不兼容
// 即使资源 `Strings` 继承了资源接口 `HasCount`，它与 `Counter` 不兼容
//
let counter2: @Counter{HasCount} <- create Strings()
```



除了限制具体类型之外，还可以限制内置类型 AnyStruct（所有结构的超类型）和 AnyResource（所有资源的超类型）。例如，受限类型 AnyResource{HasCount} 是只能使用 HasCount 资源接口功能的任何资源类型。
可以省略受限制的类型 AnyStruct 和 AnyResource。例如，类型 {HasCount} 是实现资源接口 HasCount 的任何资源。

```
pub struct interface HasID {
    pub let id: String
}

pub struct A: HasID {
    pub let id: String

    init(id: String) {
        self.id = id
    }
}

pub struct B: HasID {
    pub let id: String

    init(id: String) {
        self.id = id
    }
}

// 创建两个实例，一个类型为 `A`，一个类型为 `B`。
// 两种类型都符合`HasID`接口，所以结构体可以赋值
// 对于类型为 `AnyResource{HasID}` 的变量：只允许访问资源接口`HasID`的功能

let hasID1: {HasID} = A(id: "1")
let hasID2: {HasID} = B(id: "2")

// Declare a function named `getID` which has one parameter with type `{HasID}`.
// The type `{HasID}` is a short-hand for `AnyStruct{HasID}`:
// Some structure which only allows access to the functionality of interface `HasID`.
//
// 声明一个名为 `getID` 的函数，它有一个类型为 `{HasID}` 的参数。`{HasID}` 类型是 `AnyStruct{HasID}` 的简写：一些只允许访问接口 `HasID` 功能的结构。
//
pub fun getID(_ value: {HasID}): String {
    return value.id
}

let id1 = getID(hasID1)
// `id1` is "1"

let id2 = getID(hasID2)
// `id2` is "2"
```

只有具体类型可以被限制，例如，被限制的类型可能不是数组，类型 [T]{U} 是无效的。
当通过引用向可能不受信任的第三方程序提供对资源和结构的访问权限时，受限类型也很有用，这将在下一节中讨论。