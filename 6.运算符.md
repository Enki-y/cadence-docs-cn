# 运算符

运算符是对一个或多个值执行计算的特殊符号。它们是一元的、二元的或是三元的。

- 一元运算符对单个值执行操作。一元运算符符号出现在值之前。
- 二元运算符对两个值进行操作。二元运算符符号出现在两个值（中缀）之间。
- 三元运算符对三个值进行操作。第一个运算符符号出现在第一个和第二个值之间，第二个运算符符号出现在第二个和第三个值（中缀）之间。

## 否定

该`-`单目运算符否定一个整数：

```cadence
let a = 1
-a  // is `-1`
```

该`!`单目运算符逻辑否定一个布尔值：

```cadence
let a = true
!a  // is `false`
```

## 赋值

二元赋值运算符`=`可用于为变量分配新值。它只允许在语句中，不允许在表达式中。

```cadence
var a = 1
a = 2
// `a` is `2`


var b = 3
var c = 4

// Invalid: The assignment operation cannot be used in an expression.
a = b = c

// Instead, the intended assignment must be written in multiple statements.
b = c
a = b
```

对常量的赋值无效。

```cadence
let a = 1
// Invalid: Assignments are only for variables, not constants.
a = 2
```

赋值操作数的左侧必须是标识符。对于数组和字典，这个标识符后面可以跟一个或多个索引或访问表达式。

```cadence
// Declare an array of integers.
let numbers = [1, 2]

// Change the first element of the array.
//
numbers[0] = 3

// `numbers` is `[3, 2]`
```



```cadence
// Declare an array of arrays of integers.
let arrays = [[1, 2], [3, 4]]

// Change the first element in the second array
//
arrays[1][0] = 5

// `arrays` is `[[1, 2], [5, 4]]`
```



```cadence
let dictionaries = {
  true: {1: 2},
  false: {3: 4}
}

dictionaries[false][3] = 0

// `dictionaries` is `{
//   true: {1: 2},
//   false: {3: 0}
//}`
```

## 交换

二元交换运算符`<->`可用于交换两个变量的值。它只允许在语句中，不允许在表达式中。



```cadence
var a = 1
var b = 2
a <-> b
// `a` is `2`
// `b` is `1`

var c = 3

// Invalid: The swap operation cannot be used in an expression.
a <-> b <-> c

// Instead, the intended swap must be written in multiple statements.
b <-> c
a <-> b
```

交换操作的两边必须是可变的，赋值给常量是无效的。



```cadence
var a = 1
let b = 2

// Invalid: Swapping is only possible for variables, not constants.
a <-> b
```

交换操作的两边必须是一个标识符，后跟一个或多个索引或访问表达式。

## 算术

有四种算术运算符：

- 添加： `+`
- 减法： `-`
- 乘法： `*`
- 分配： `/`
- 余： `%`



```cadence
let a = 1 + 2
// `a` is `3`
```

运算符的参数需要是相同的类型。结果始终与参数类型相同。

当除数为零时，除法和余数运算符会中止程序。

有符号整数类型`Int8`, `Int16`, `Int32`, `Int64`, `Int128`,`Int256`和无符号整数类型 `UInt8`, `UInt16`, `UInt32`, `UInt64`, `UInt128`, 的算术运算 `UInt256`不会导致值上溢或下溢。



```cadence
let a: UInt8 = 255

// Run-time error: The result `256` does not fit in the range of `UInt8`,
// thus a fatal overflow error is raised and the program aborts
//
let b = a + 1
```



```cadence
let a: Int8 = 100
let b: Int8 = 100

// Run-time error: The result `10000` does not fit in the range of `Int8`,
// thus a fatal overflow error is raised and the program aborts
//
let c = a * b
```



```cadence
let a: Int8 = -128

// Run-time error: The result `128` does not fit in the range of `Int8`,
// thus a fatal overflow error is raised and the program aborts
//
let b = -a
```

在无符号整数类型的算术运算 `Word8`，`Word16`，`Word32`，`Word64` 可能会导致值溢或下溢。

例如，无符号 8 位整数的最大值为 255（二进制 11111111）。加 1 导致溢出，截断为 8 位，值为 0。



```cadence
//    11111111 = 255
// +         1
// = 100000000 = 0
```



```cadence
let a: Word8 = 255
a + 1 // is `0`
```

类似地，对于最小值 0，减去 1 会产生最大值 255。



```cadence
//    00000000
// -         1
// =  11111111 = 255
```



```cadence
let b: Word8 = 0
b - 1  // is `255`
```

## 逻辑运算符

逻辑运算符处理布尔值`true`和`false`。

- 逻辑与： `a && b`

  ```cadence
  true && true  // is `true`
  
  true && false  // is `false`
  
  false && true  // is `false`
  
  false && false  // is `false`
  ```

  如果左侧为假，则不评估右侧。

- 逻辑或： `a || b`

  ```cadence
  true || true  // is `true`
  
  true || false  // is `true`
  
  false || true  // is `true`
  
  false || false // is `false`
  ```

  如果左侧为真，则不评估右侧。

## 比较运算符

比较运算符处理布尔值和整数值。

- Equality: `==`, 对于布尔值和整数

  等式运算符的两边都可以是可选的，甚至是不同级别的，因此例如可以将非可选与双可选 ( `??`) 进行比较。

  ```cadence
  1 == 1  // is `true`
  
  1 == 2  // is `false`
  ```

  

  ```cadence
  true == true  // is `true`
  
  true == false  // is `false`
  ```

  

  ```cadence
  let x: Int? = 1
  x == nil  // is `false`
  ```

  

  ```cadence
  let x: Int = 1
  x == nil  // is `false`
  ```

  

  ```cadence
  // Comparisons of different levels of optionals are possible.
  let x: Int? = 2
  let y: Int?? = nil
  x == y  // is `false`
  ```

  

  ```cadence
  // Comparisons of different levels of optionals are possible.
  let x: Int? = 2
  let y: Int?? = 2
  x == y  // is `true`
  ```

- 不等式：`!=`, 对于布尔值和整数（可能是可选的）

  不等式运算符的两边都可以是可选的，甚至是不同级别的，因此例如可以将非可选与双可选 ( `??`) 进行比较。

  

  ```cadence
  1 != 1  // is `false`
  
  1 != 2  // is `true`
  ```

  

  ```cadence
  true != true  // is `false`
  
  true != false  // is `true`
  ```

  

  ```cadence
  let x: Int? = 1
  x != nil  // is `true`
  ```

  

  ```cadence
  let x: Int = 1
  x != nil  // is `true`
  ```

  

  ```cadence
  // Comparisons of different levels of optionals are possible.
  let x: Int? = 2
  let y: Int?? = nil
  x != y  // is `true`
  ```

  

  ```cadence
  // Comparisons of different levels of optionals are possible.
  let x: Int? = 2
  let y: Int?? = 2
  x != y  // is `false`
  ```

- 小于：`<`，对于整数

  

  ```cadence
  1 < 1  // is `false`
  
  1 < 2  // is `true`
  
  2 < 1  // is `false`
  ```

- 小于或等于：`<=`，对于整数

  

  ```cadence
  1 <= 1  // is `true`
  
  1 <= 2  // is `true`
  
  2 <= 1  // is `false`
  ```

- 大于：`>`，对于整数

  

  ```cadence
  1 > 1  // is `false`
  
  1 > 2  // is `false`
  
  2 > 1  // is `true`
  ```

- 大于或等于：`>=`，对于整数

  

  ```cadence
  1 >= 1  // is `true`
  
  1 >= 2  // is `false`
  
  2 >= 1  // is `true`
  ```

## 三元条件运算符

只有一种三元条件运算符，即三元条件运算符 ( `a ? b : c`)。

它的行为类似于 if 语句，但它是一个表达式：如果第一个运算符值为 true，则返回第二个运算符值。如果第一个运算符值为 false，则返回第三个值。

第一个值必须是布尔值（必须具有类型`Bool`）。第二个值和第三个值可以是任何类型。结果类型是第二个和第三个值的最不常见的超类型。



```cadence
let x = 1 > 2 ? 3 : 4
// `x` is `4` and has type `Int`

let y = 1 > 2 ? nil : 3
// `y` is `3` and has type `Int?`
```

## 优先级和关联性

运算符具有以下优先级，从高到低：

- 一元优先级：`-`, `!`,`<-`
- 投影优先级：`as`, `as?`,`as!`
- 乘法优先级：`*`, `/`,`%`
- 加减法优先级：`+`,`-`
- 按位移位优先级：`<<`,`>>`
- 按位连接优先级： `&`
- 按位互斥析取优先级： `^`
- 按位析取优先级： `|`
- 零合并优先级： `??`
- 关系优先级：`<`, `<=`, `>`,`>=`
- 平等优先：`==`,`!=`
- 逻辑连词优先级： `&&`
- 逻辑析取优先级： `||`
- 三元优先级： `? :`

所有运算符都是左关联的，除了以下是右关联的运算符：

- 三元运算符
- 零合并算子

表达式可以用括号括起来以覆盖优先约定，即应指示替代顺序，或何时应强调默认顺序，例如以避免混淆。例如，`(2 + 3) * 4`强制加法先于乘法，并`5 + (6 * 7)`加强默认顺序。