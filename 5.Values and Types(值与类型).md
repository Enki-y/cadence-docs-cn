# 值和类型

值是对象，例如布尔值、整数或数组。值是有类型的。

## 布尔值

`Bool`类型包含两个布尔值`true`和`false`。

## 数值

数字可以有多种表示形式。默认情况下，数字被假定为十进制。非十进制文字有一个特定的前缀。

| 数制         | 字首 | 人物                                     |
| ------------ | ---- | ---------------------------------------- |
| **十进制**   | 无   | 一个或多个数字（`0`到`9`）               |
| **二进制**   | `0b` | 一个或多个零或一（`0`或`1`）             |
| **八进制**   | `0o` | 在范围内的一个或多个数字`0`到`7`         |
| **十六进制** | `0x` | 一个或多个数字或字符`a`到`f`、小写或大写 |



```cadence
// 一个十进制的数字
//
1234567890  // is `1234567890`

// 一个二进制数字
//
0b101010  // is `42`

// 一个八进制数字
//
0o12345670  // is `2739128`

// 一个十六进制数字
//
0x1234567890ABCabc  // is `1311768467294898876`

// Invalid(不合法的): 不支持的前缀 0z
//
0z0

// 一个以0开头的十进制数字，而不是八进制！
00123 // is `123`

// 在后面有几个0的二进制数字
0b001000  // is `8`
```

十进制数字可能包含下划线 ( `_`) 以在逻辑上分隔组件。

```cadence
let largeNumber = 1_000_000

// Invalid: Value is not a number literal, but a variable.
let notNumber = _123
```

所有数字系统都允许使用下划线。

```cadence
let binaryNumber = 0b10_11_01
```

## 整数

整数是没有小数部分的数字。它们有*符号*（正、零或负）或*无符号*（正或零）。

检查上溢和下溢的有符号整数类型有一个`Int`前缀，可以表示以下范围内的值：

- `Int8`: −2^7 到 2^7 − 1 (-128 到 127)
- `Int16`: −2^15 到 2^15 − 1 (-32768 到 32767)
- `Int32`: −2^31 到 2^31 − 1 (-2147483648 到 2147483647)
- `Int64`: −2^63 到 2^63 − 1 (-9223372036854775808 到 9223372036854775807)
- `Int128`: −2^127 到 2^127 − 1
- `Int256`: −2^255 到 2^255 − 1

检查上溢和下溢的无符号整数类型有一个`UInt`前缀，可以表示以下范围内的值：

- `UInt8`: 0 到 2^8 − 1 (255)
- `UInt16`: 0 到 2^16 − 1 (65535)
- `UInt32`: 0 到 2^32 − 1 (4294967295)
- `UInt64`: 0 到 2^64 − 1 (18446744073709551615)
- `UInt128`: 0 到 2^128 − 1
- `UInt256`: 0 到 2^256 − 1

**不**检查上溢和下溢（即环绕）的无符号整数类型具有`Word`前缀并且可以表示以下范围内的值：

- `Word8`: 0 到 2^8 − 1 (255)
- `Word16`: 0 到 2^16 − 1 (65535)
- `Word32`: 0 到 2^32 − 1 (4294967295)
- `Word64`: 0 到 2^64 − 1 (18446744073709551615)

这些类型是独立的类型，即不是彼此的子类型。

有关不同整数类型行为的更多信息，请参阅关于[算术运算符](https://docs.onflow.org/cadence/language/operators#arithmetic)的部分。

```cadence
// Declare a constant that has type `UInt8` and the value 10.
let smallNumber: UInt8 = 10
```

```cadence
// Invalid: negative literal cannot be used as an unsigned integer
//
let invalidNumber: UInt8 = -10
```

此外，`Int`还提供了任意精度整数类型。

```cadence
let veryLargeNumber: Int = 10000000000000000000000000000000
```

[推断](https://docs.onflow.org/cadence/language/type-inference)整数文字具有 type `Int`，或者如果文字出现在需要显式类型的位置，例如在具有显式类型注释的变量声明中。

```cadence
let someNumber = 123

// `someNumber` has type `Int`
```

负整数以二进制补码表示进行编码。

整数类型不会自动转换。必须显式转换类型，这可以通过调用具有整数类型的类型的构造函数来完成。

```cadence
let x: Int8 = 1
let y: Int16 = 2

// Invalid: the types of the operands, `Int8` and `Int16` are incompatible.
let z = x + y

// Explicitly convert `x` from `Int8` to `Int16`.
let a = Int16(x) + y

// `a` has type `Int16`

// Invalid: The integer literal is expected to be of type `UInt8`,
// but the large integer literal does not fit in the range of `UInt8`.
//
let b = x + 1000000000000000000000000
```

### 整数函数

整数有多个可以使用的内置函数。

- 

  ```cadence
  fun toString(): String
  ```

  返回整数的字符串表示形式。

  ```cadence
  let answer = 42
  
  answer.toString()  // is "42"
  ```

- 

  ```cadence
  fun toBigEndianBytes(): [UInt8]
  ```

  `[UInt8]`以整数的大端顺序返回字节数组表示形式 ( )。

  ```cadence
  let largeNumber = 1234567890
  
  largeNumber.toBigEndianBytes()  // is `[73, 150, 2, 210]`
  ```

## 定点数

🚧 状态：目前只有 64 位宽`Fix64`和`UFix64`类型可用。更多定点数类型将在未来版本中添加。

定点数可用于表示小数值。它们在小数点后有固定的位数。

它们本质上是按因子缩放的整数。例如，值 1.23 可以表示为 1230，缩放因子为 1/1000。相同类型的所有值的比例因子相同，并且在计算过程中保持不变。

Cadence 中的定点数有一个 10 次幂的比例因子，而不是 2 次幂，即它们是十进制的，而不是二进制的。

有符号定点数类型具有前缀`Fix`，具有以下因子，并且可以表示以下范围内的值：

- `Fix64`：系数 1/100,000,000；-92233720368.54775808 至 92233720368.54775807

无符号定点数类型具有前缀`UFix`，具有以下因子，并且可以表示以下范围内的值：

- `UFix64`：系数 1/100,000,000；0.0 到 184467440737.09551615

### 定点数函数

定点数具有多个可以使用的内置函数。

- 

  ```cadence
  fun toString(): String
  ```

  返回定点数的字符串表示形式。

  ```cadence
  let fix = 1.23
  
  fix.toString()  // is "1.23000000"
  ```

- 

  ```cadence
  fun toBigEndianBytes(): [UInt8]
  ```

  `[UInt8]`以定点数的大端顺序返回字节数组表示形式 ( )。

  ```cadence
  let fix = 1.23
  
  fix.toBigEndianBytes()  // is `[0, 0, 0, 0, 7, 84, 212, 192]`
  ```

## 最小值和最大值

所有整数和定点数类型的最小值和最大值可通过字段`min`和 获得`max`。

例如：

```cadence
let max = UInt8.max
// `max` is 255, the maximum value of the type `UInt8`
```

```cadence
let max = UFix64.max
// `max` is 184467440737.09551615, the maximum value of the type `UFix64`
```

## 饱和算法

整数和定点数支持饱和算术：算术运算（例如加法或乘法）在数值边界处饱和而不是溢出。

如果运算结果大于操作数类型的最大值，则返回最大值。如果结果低于操作数类型的最小值，则返回最小值。

饱和加法、减法、乘法和除法作为带有前缀的函数提供`saturating`：

- `Int8`, `Int16`, `Int32`, `Int64`, `Int128`, `Int256`, `Fix64`:
  - `saturatingAdd`
  - `saturatingSubtract`
  - `saturatingMultiply`
  - `saturatingDivide`
- `Int`：
  - None
- `UInt8`, `UInt16`, `UInt32`, `UInt64`, `UInt128`, `UInt256`, `UFix64`:
  - `saturatingAdd`
  - `saturatingSubtract`
  - `saturatingMultiply`
- `UInt`：
  - `saturatingSubtract`

```cadence
let a: UInt8 = 200
let b: UInt8 = 100
let result = a.saturatingAdd(b)
// `result` is 255, the maximum value of the type `UInt8`
```

## 浮点数字

没有对浮点数的支持。

智能合约不适用于具有误差范围的值，因此浮点运算在这里不合适。

相反，请考虑使用[定点数](https://docs.onflow.org/cadence/language/values-and-types/#fixed-point-numbers)。

## 地址

类型`Address`代表一个地址。地址是大小为 64 位（8 字节）的无符号整数。十六进制整数文字可用于创建地址值。



```cadence
// Declare a constant that has type `Address`.
//
let someAddress: Address = 0x436164656E636521

// Invalid: Initial value is not compatible with type `Address`,
// it is not a number.
//
let notAnAddress: Address = ""

// Invalid: Initial value is not compatible with type `Address`.
// The integer literal is valid, however, it is larger than 64 bits.
//
let alsoNotAnAddress: Address = 0x436164656E63652146757265766572
```

整数文字不被推断为地址。

```cadence
// Declare a number. Even though it happens to be a valid address,
// it is not inferred as it.
//
let aNumber = 0x436164656E636521

// `aNumber` has type `Int`
```

### 地址函数

地址具有多个您可以使用的内置函数。

- 

  ```cadence
  fun toString(): String
  ```

  

  返回地址的字符串表示形式。

  ```cadence
  let someAddress: Address = 0x436164656E636521
  
  someAddress.toString()  // is "0x436164656E636521"
  ```

- 

  ```cadence
  fun toBytes(): [UInt8]
  ```

  返回地址的字节数组表示 ( `[UInt8]`)。

  ```cadence
  let someAddress: Address = 0x436164656E636521
  
  someAddress.toBytes()  // is `[67, 97, 100, 101, 110, 99, 101, 33]`
  ```

## AnyStruct 和 AnyResource

`AnyStruct` 是所有非资源类型的顶级类型，即所有非资源类型都是它的子类型。

`AnyResource` 是所有资源类型的顶级类型。

```cadence
// Declare a variable that has the type `AnyStruct`.
// Any non-resource typed value can be assigned to it, for example an integer,
// but not resource-typed values.
//
var someStruct: AnyStruct = 1

// Assign a value with a different non-resource type, `Bool`.
someStruct = true

// Declare a structure named `TestStruct`, create an instance of it,
// and assign it to the `AnyStruct`-typed variable
//
struct TestStruct {}

let testStruct = TestStruct()

someStruct = testStruct

// Declare a resource named `TestResource`

resource Test {}

// Declare a variable that has the type `AnyResource`.
// Any resource-typed value can be assigned to it,
// but not non-resource typed values.
//
var someResource: @AnyResource <- create Test()

// Invalid: Resource-typed values can not be assigned
// to `AnyStruct`-typed variables
//
someStruct <- create Test()

// Invalid: Non-resource typed values can not be assigned
// to `AnyResource`-typed variables
//
someResource = 1
```

但是，使用`AnyStruct`并且`AnyResource`不会选择退出类型检查。在这些类型上访问字段和调用函数是无效的，因为它们没有字段和函数。

```cadence
// Declare a variable that has the type `AnyStruct`.
// The initial value is an integer,
// but the variable still has the explicit type `AnyStruct`.
//
let a: AnyStruct = 1

// Invalid: Operator cannot be used for an `AnyStruct` value (`a`, left-hand side)
// and an `Int` value (`2`, right-hand side).
//
a + 2
```

`AnyStruct`并且`AnyResource`可以像其他类型一样使用，例如，它们可以是[数组](https://docs.onflow.org/cadence/language/values-and-types/#arrays)的元素类型，也可以是[可选类型](https://docs.onflow.org/cadence/language/values-and-types/#optionals) 的元素[类型](https://docs.onflow.org/cadence/language/values-and-types/#optionals)。

```cadence
// Declare a variable that has the type `[AnyStruct]`,
// i.e. an array of elements of any non-resource type.
//
let anyValues: [AnyStruct] = [1, "2", true]

// Declare a variable that has the type `AnyStruct?`,
// i.e. an optional type of any non-resource type.
//
var maybeSomething: AnyStruct? = 42

maybeSomething = "twenty-four"

maybeSomething = nil
```

`AnyStruct`也是所有非资源可选类型`AnyResource`的超类型，是所有资源可选类型的超类型。



```cadence
let maybeInt: Int? = 1
let anything: AnyStruct = maybeInt
```

[有条件的向下转换](https://docs.onflow.org/cadence/language/operators#conditional-downcasting-operator)允许胁迫具有类型的值`AnyStruct`或`AnyResource`到其原始型背。

## 可选

Optionals 是可以表示没有值的值。Optionals 有两种情况：要么有值，要么什么都没有。

可选类型是使用`?`另一种类型的后缀声明的。例如，`Int`是一个非可选整数，并且`Int?`是一个可选整数，即要么没有，要么是一个整数。

代表无的值是`nil`。

```cadence
// Declare a constant which has an optional integer type,
// with nil as its initial value.
//
let a: Int? = nil

// Declare a constant which has an optional integer type,
// with 42 as its initial value.
//
let b: Int? = 42

// Invalid: `b` has type `Int?`, which does not support arithmetic.
b + 23

// Invalid: Declare a constant with a non-optional integer type `Int`,
// but the initial value is `nil`, which in this context has type `Int?`.
//
let x: Int = nil
```

可以为任何值创建可选项，而不仅仅是为文字。

```cadence
// Declare a constant which has a non-optional integer type,
// with 1 as its initial value.
//
let x = 1

// Declare a constant which has an optional integer type.
// An optional with the value of `x` is created.
//
let y: Int? = x

// Declare a variable which has an optional any type, i.e. the variable
// may be `nil`, or any other value.
// An optional with the value of `x` is created.
//
var z: AnyStruct? = x
```

非可选类型是其可选类型的子类型。

```cadence
var a: Int? = nil
let b = 2
a = b

// `a` is `2`
```

可选类型可能包含在其他类型中，例如[数组](https://docs.onflow.org/cadence/language/values-and-types/#arrays)甚至可选类型。

```cadence
// Declare a constant which has an array type of optional integers.
let xs: [Int?] = [1, nil, 2, nil]

// Declare a constant which has a double optional type.
//
let doubleOptional: Int?? = nil
```

### 空合运算符

`??`如果可选值包含值，则nil 合并运算符返回可选值，如果可选值没有值，即可选值是 ，则返回替代值`nil`。

如果左侧非零，则不评估右侧。

```cadence
// Declare a constant which has an optional integer type
//
let a: Int? = nil

// Declare a constant with a non-optional integer type,
// which is initialized to `a` if it is non-nil, or 42 otherwise.
//
let b: Int = a ?? 42
// `b` is 42, as `a` is nil
```

零合并运算符只能应用于具有可选类型的值。



```cadence
// Declare a constant with a non-optional integer type.
//
let a = 1

// Invalid: nil-coalescing operator is applied to a value which has a non-optional type
// (a has the non-optional type `Int`).
//
let b = a ?? 2
```



```cadence
// Invalid: nil-coalescing operator is applied to a value which has a non-optional type
// (the integer literal is of type `Int`).
//
let c = 1 ?? 2
```

运算符右侧的类型（替代值）必须是左侧类型的子类型，即运算符右侧必须是与类型匹配的非可选或可选类型左侧的。



```cadence
// Declare a constant with an optional integer type.
//
let a: Int? = nil
let b: Int? = 1
let c = a ?? b
// `c` is `1` and has type `Int?`

// Invalid: nil-coalescing operator is applied to a value of type `Int?`,
// but the alternative has type `Bool`.
//
let d = a ?? false
```

### 强制展开 ( `!`)

force-unwrap 操作符 ( `!`) 如果它包含一个值，则返回一个可选项中的值，或者如果可选项没有值，即可选值是 ，则恐慌并中止执行`nil`。



```cadence
// Declare a constant which has an optional integer type
//
let a: Int? = nil

// Declare a constant with a non-optional integer type,
// which is initialized to `a` if `a` is non-nil.
// If `a` is nil, the program aborts.
//
let b: Int = a!
// The program aborts because `a` is nil.

// Declare another optional integer constant
let c: Int? = 3

// Declare a non-optional integer
// which is initialized to `c` if `c` is non-nil.
// If `c` is nil, the program aborts.
let d: Int = c!
// `d` is initialized to 3 because c isn't nil.
```

force-unwrap 运算符只能应用于具有可选类型的值。



```cadence
// Declare a constant with a non-optional integer type.
//
let a = 1

// Invalid: force-unwrap operator is applied to a value which has a
// non-optional type (`a` has the non-optional type `Int`).
//
let b = a!
```



```cadence
// Invalid: The force-unwrap operator is applied
// to a value which has a non-optional type
// (the integer literal is of type `Int`).
//
let c = 1!
```

### 力赋值运算符 ( `<-!`)

`<-!`如果变量为零，则强制赋值运算符 ( ) 将资源类型值分配给可选类型变量。如果分配给的变量非零，则程序的执行中止。

force-assignment 运算符仅用于 [资源类型](https://docs.onflow.org/cadence/language/composite-types#resources)和移动运算符 ( `<-`)，这在本文档的资源部分进行了介绍。

### 条件向下转换运算符

条件向下转换运算符`as?`可用于将值转换为类型。运算符返回一个可选的。如果该值的类型是应强制转换为给定类型的子类型，则运算符将值作为给定类型返回，否则结果为`nil`。

强制转换和检查是在运行时执行的，即执行程序时，而不是静态执行的，即检查程序时。



```cadence
// Declare a constant named `something` which has type `AnyStruct`,
// with an initial value which has type `Int`.
//
let something: AnyStruct = 1

// Conditionally downcast the value of `something` to `Int`.
// The cast succeeds, because the value has type `Int`.
//
let number = something as? Int
// `number` is `1` and has type `Int?`

// Conditionally downcast the value of `something` to `Bool`.
// The cast fails, because the value has type `Int`,
// and `Bool` is not a subtype of `Int`.
//
let boolean = something as? Bool
// `boolean` is `nil` and has type `Bool?`
```

向下转换适用于嵌套类型（例如数组）、接口（如果[资源](https://docs.onflow.org/cadence/language/composite-types#resources)接口不是具体资源）和选项。



```cadence
// Declare a constant named `values` which has type `[AnyStruct]`,
// i.e. an array of arbitrarily typed values.
//
let values: [AnyStruct] = [1, true]

let first = values[0] as? Int
// `first` is `1` and has type `Int?`

let second = values[1] as? Bool
// `second` is `true` and has type `Bool?`
```

## 绝不

`Never`是底层类型，即它是所有类型的子类型。没有具有 type 的值`Never`。 `Never`可以用作永远不会正常返回的函数的返回类型。例如，它是函数的返回类型[`panic`](https://docs.onflow.org/cadence/language/built-in-functions#panic)。



```cadence
// Declare a function named `crashAndBurn` which will never return,
// because it calls the function named `panic`, which never returns.
//
fun crashAndBurn(): Never {
    panic("An unrecoverable error occurred")
}

// Invalid: Declare a constant with a `Never` type, but the initial value is an integer.
//
let x: Never = 1

// Invalid: Declare a function which returns an invalid return value `nil`,
// which is not a value of type `Never`.
//
fun returnNever(): Never {
    return nil
}
```

## 字符串和字符

字符串是字符的集合。字符串有类型`String`，字符有类型`Character`。字符串可用于以符合 Unicode 的方式处理文本。字符串是不可变的。

字符串和字符文字用双引号 ( `"`)括起来。



```cadence
let someString = "Hello, world!"
```

字符串文字可能包含转义序列。转义序列以反斜杠 ( `\`)开头：

- `\0`: 空字符
- `\\`: 反斜杠
- `\t`: 水平标签
- `\n`: 换行
- `\r`: 回车
- `\"`: 双引号
- `\'`: 单引号
- `\u`: 一个 Unicode 标量值，写成`\u{x}`，其中`x`1-8 位十六进制数需要是一个有效的 Unicode 标量值，即在 0 到 0xD7FF 和 0xE000 到 0x10FFFF 的范围内



```cadence
// Declare a constant which contains two lines of text
// (separated by the line feed character `\n`), and ends
// with a thumbs up emoji, which has code point U+1F44D (0x1F44D).
//
let thumbsUpText =
    "This is the first line.\nThis is the second line with an emoji: \u{1F44D}"
```

该类型`Character`表示单个人类可读的字符。字符是扩展的字素簇，由一个或多个 Unicode 标量组成。

例如，单个字符`ü`可以在 Unicode 中以多种方式表示。首先，它可以由单个 Unicode 标量值`ü` （“LATIN SMALL LETTER U WITH DIAERESIS”，代码点 U+00FC）表示。其次，相同的单个字符可以由两个 Unicode 标量值表示：（ `u`“LATIN SMALL LETTER U”，代码点 U+0075）和“COMBINING DIAERESIS”（代码点 U+0308）。组合 Unicode 标量值应用于它之前的标量，这将 a`u`变成 a `ü`。

尽管如此，这两种变体都代表相同的人类可读字符`ü`。



```cadence
let singleScalar: Character = "\u{FC}"
// `singleScalar` is `ü`
let twoScalars: Character = "\u{75}\u{308}"
// `twoScalars` is `ü`
```

将多个 Unicode 标量值呈现为单个人类可读字符的另一个示例是标志表情符号。这些表情符号由两个“区域指示符符号字母”Unicode 标量值组成。



```cadence
// Declare a constant for a string with a single character, the emoji
// for the Canadian flag, which consists of two Unicode scalar values:
// - REGIONAL INDICATOR SYMBOL LETTER C (U+1F1E8)
// - REGIONAL INDICATOR SYMBOL LETTER A (U+1F1E6)
//
let canadianFlag: Character = "\u{1F1E8}\u{1F1E6}"
// `canadianFlag` is `🇨🇦`
```

### 字符串字段和函数

字符串有多个可以使用的内置函数：

- 

  ```cadence
  let length: Int
  ```

  

  以整数形式返回字符串中的字符数。

  

  ```cadence
  let example = "hello"
  
  // Find the number of elements of the string.
  let length = example.length
  // `length` is `5`
  ```

- 

  ```cadence
  let utf8: [UInt8]
  ```

  

  UTF-8 编码的字节数组

  

  ```cadence
  let flowers = "Flowers \u{1F490}"
  let bytes = flowers.utf8
  // `bytes` is `[70, 108, 111, 119, 101, 114, 115, 32, 240, 159, 146, 144]`
  ```

- 

  ```cadence
  fun concat(_ other: String): String
  ```

  

  将字符串连接`other`到原始字符串的末尾，但不修改原始字符串。此函数创建一个新字符串，其长度是调用该函数的字符串长度与作为参数给出的字符串长度之和。

  

  ```cadence
  let example = "hello"
  let new = "world"
  
  // Concatenate the new string onto the example string and return thenew string.
  let helloWorld = example.concat(new)
  // `helloWorld` is now `"helloworld"`
  ```

- 

  ```cadence
  fun slice(from: Int, upTo: Int): String
  ```

  

  返回给定字符串中从开始索引`from`到但不包括结束索引的字符的字符串切片`upTo`。此函数创建一个长度为 的新字符串`upTo - from`。它不会修改原始字符串。如果其中一个参数超出字符串的边界，则函数将失败。

  

  ```cadence
  let example = "helloworld"
  
  // Create a new slice of part of the original string.
  let slice = example.slice(from: 3, upTo: 6)
  // `slice` is now `"lowo"`
  
  // Run-time error: Out of bounds index, the program aborts.
  let outOfBounds = example.slice(from: 2, upTo: 10)
  ```

- 

  ```cadence
  fun decodeHex(): [UInt8]
  ```

  

  返回一个包含由给定十六进制字符串表示的字节的数组。

  给定的字符串只能包含十六进制字符并且必须具有偶数长度。如果字符串格式错误，程序将中止

  

  ```cadence
  let example = "436164656e636521"
  
  example.decodeHex()  // is `[67, 97, 100, 101, 110, 99, 101, 33]`
  ```

该`String`类型还提供以下功能：

- 

  ```cadence
  fun String.encodeHex(_ data: [UInt8]): String
  ```

  

  返回给定字节数组的十六进制字符串

  

  ```cadence
  let data = [1 as UInt8, 2, 3, 0xCA, 0xDE]
  
  String.encodeHex(data)  // is `"010203cade"`
  ```

## 数组

数组是可变的、有序的值集合。数组可能多次包含一个值。数组文字以左方括号开头`[`，以右方括号结尾`]`。



```cadence
// An empty array
//
[]

// An array with integers
//
[1, 2, 3]
```

### 数组类型

数组要么具有固定大小，要么具有可变大小，即可以添加和删除元素。

固定大小的数组类型的形式为`[T; N]`，其中`T`是元素类型，`N`是数组的大小。 `N`必须是静态已知的，这意味着它需要是一个整数文字。例如，一个包含 3 个`Int8`元素的固定大小数组的类型为`[Int8; 3]`。

可变大小数组类型的形式为`[T]`，其中`T`是元素类型。例如， type`[Int16]`指定了一个由 type元素组成的可变大小数组`Int16`。

数组中的所有值都必须具有一个类型，该类型是数组元素类型 ( `T`)的子类型。

了解数组是值类型很重要，并且仅在用作常量或变量的初始值、分配给变量时、用作函数参数时或从函数调用返回时才被复制。



```cadence
let size = 2
// Invalid: Array-size must be an integer literal
let numbers: [Int; size] = []

// Declare a fixed-sized array of integers
// which always contains exactly two elements.
//
let array: [Int8; 2] = [1, 2]

// Declare a fixed-sized array of fixed-sized arrays of integers.
// The inner arrays always contain exactly three elements,
// the outer array always contains two elements.
//
let arrays: [[Int16; 3]; 2] = [
    [1, 2, 3],
    [4, 5, 6]
]

// Declare a variable length array of integers
var variableLengthArray: [Int] = []

// Mixing values with different types is possible
// by declaring the expected array type
// with the common supertype of all values.
//
let mixedValues: [AnyStruct] = ["some string", 42]
```

数组类型的元素类型是协变的。例如，`[Int]`是 的子类型`[AnyStruct]`。这是安全的，因为数组是值类型而不是引用类型。

### 数组索引

要获取特定索引处的数组元素，可以使用索引语法：数组后跟左方括号`[`、索引值，并以右方括号 结束`]`。

数组中第一个元素的索引从 0 开始。

访问越界的元素会导致运行时出现致命错误并中止程序。



```cadence
// Declare an array of integers.
let numbers = [42, 23]

// Get the first number of the array.
//
numbers[0] // is `42`

// Get the second number of the array.
//
numbers[1] // is `23`

// Run-time error: Index 2 is out of bounds, the program aborts.
//
numbers[2]
```



```cadence
// Declare an array of arrays of integers, i.e. the type is `[[Int]]`.
let arrays = [[1, 2], [3, 4]]

// Get the first number of the second array.
//
arrays[1][0] // is `3`
```

要在特定索引处设置数组元素，也可以使用索引语法。

```cadence
// Declare an array of integers.
let numbers = [42, 23]

// Change the second number in the array.
//
// NOTE: The declaration `numbers` is constant, which means that
// the *name* is constant, not the *value* – the value, i.e. the array,
// is mutable and can be changed.
//
numbers[1] = 2

// `numbers` is `[42, 2]`
```

### 数组字段和函数

数组具有多个内置字段和函数，可用于获取有关数组内容的信息并对其进行操作。

字段`length`、 函数`concat`、 和`contains` 可用于可变大小和固定大小或可变大小的数组。

- 

  ```cadence
  let length: Int
  ```

  

  数组中的元素数。

  ```cadence
  // Declare an array of integers.
  let numbers = [42, 23, 31, 12]
  
  // Find the number of elements of the array.
  let length = numbers.length
  
  // `length` is `4`
  ```
  
- 

  ```cadence
  fun concat(_ array: T): T
  ```

  

  将参数连接`array`到调用函数的数组的末尾，但不修改该数组。

  两个数组的类型必须相同`T`。

  此函数创建一个新数组，其长度是调用该函数的数组长度与作为参数给出的数组长度之和。

  ```cadence
  // Declare two arrays of integers.
  let numbers = [42, 23, 31, 12]
  let moreNumbers = [11, 27]
  
  // Concatenate the array `moreNumbers` to the array `numbers`
  // and declare a new variable for the result.
  //
  let allNumbers = numbers.concat(moreNumbers)
  
  // `allNumbers` is `[42, 23, 31, 12, 11, 27]`
  // `numbers` is still `[42, 23, 31, 12]`
  // `moreNumbers` is still `[11, 27]`
  ```
  
- 

  ```cadence
  fun contains(_ element: T): Bool
  ```

  

  如果给定的 type 元素`T`在数组中，则返回 true 。

  ```cadence
  // Declare an array of integers.
  let numbers = [42, 23, 31, 12]
  
  // Check if the array contains 11.
  let containsEleven = numbers.contains(11)
  // `containsEleven` is `false`
  
  // Check if the array contains 12.
  let containsTwelve = numbers.contains(12)
  // `containsTwelve` is `true`
  
  // Invalid: Check if the array contains the string "Kitty".
  // This results in a type error, as the array only contains integers.
  //
  let containsKitty = numbers.contains("Kitty")
  ```

#### 可变大小的数组函数

以下函数只能用于可变大小的数组。在固定大小的数组上使用这些函数之一是无效的。

- 

  ```cadence
  fun append(_ element: T): Void
  ```

  

  将`element`type的新元素添加`T`到数组的末尾。

  新元素的类型必须与数组中所有其他元素的类型相同。

  ```cadence
  // Declare an array of integers.
  let numbers = [42, 23, 31, 12]
  
  // Add a new element to the array.
  numbers.append(20)
  // `numbers` is now `[42, 23, 31, 12, 20]`
  
  // Invalid: The parameter has the wrong type `String`.
  numbers.append("SneakyString")
  ```
  
- 

  ```cadence
  fun appendAll(_ array: T): Void
  ```

  

  将所有元素添加`array`到调用函数的数组末尾。

  两个数组的类型必须相同`T`。

  ```cadence
  // Declare an array of integers.
  let numbers = [42, 23]
  
  // Add new elements to the array.
  numbers.appendAll([31, 12, 20])
  // `numbers` is now `[42, 23, 31, 12, 20]`
  
  // Invalid: The parameter has the wrong type `[String]`.
  numbers.appendAll(["Sneaky", "String"])
  ```
  
- 

  ```cadence
  fun insert(at index: Int, _ element: T): Void
  ```

  

  在数组 的给定处插入`element`type的新元素。`T``index`

  新元素的类型必须与数组中的其他元素的类型相同。

  的`index`必须是该阵列的界限内。如果索引超出范围，则程序中止。

  提供的索引处的现有元素不会被覆盖。

  新插入元素之后的所有元素都向右移动一位。

  ```cadence
  // Declare an array of integers.
  let numbers = [42, 23, 31, 12]
  
  // Insert a new element at position 1 of the array.
  numbers.insert(at: 1, 20)
  // `numbers` is now `[42, 20, 23, 31, 12]`
  
  // Run-time error: Out of bounds index, the program aborts.
  numbers.insert(at: 12, 39)
  ```
  
- 

  ```cadence
  fun remove(at index: Int): T
  ```

  

  `index`从数组中删除给定的元素并返回它。

  的`index`必须是该阵列的界限内。如果索引超出范围，则程序中止。

  ```cadence
  // Declare an array of integers.
  let numbers = [42, 23, 31]
  
  // Remove element at position 1 of the array.
  let twentyThree = numbers.remove(at: 1)
  // `numbers` is now `[42, 31]`
  // `twentyThree` is `23`
  
  // Run-time error: Out of bounds index, the program aborts.
  numbers.remove(at: 19)
  ```
  
- 

  ```cadence
  fun removeFirst(): T
  ```

  

  从数组中移除第一个元素并返回它。

  数组不能为空。如果数组为空，则程序中止。

  ```cadence
  // Declare an array of integers.
  let numbers = [42, 23]
  
  // Remove the first element of the array.
  let fortytwo = numbers.removeFirst()
  // `numbers` is now `[23]`
  // `fortywo` is `42`
  
  // Remove the first element of the array.
  let twentyThree = numbers.removeFirst()
  // `numbers` is now `[]`
  // `twentyThree` is `23`
  
  // Run-time error: The array is empty, the program aborts.
  numbers.removeFirst()
  ```
  
- 

  ```cadence
  fun removeLast(): T
  ```

  

  从数组中删除最后一个元素并返回它。

  数组不能为空。如果数组为空，则程序中止。

  ```cadence
  // Declare an array of integers.
  let numbers = [42, 23]
  
  // Remove the last element of the array.
  let twentyThree = numbers.removeLast()
  // `numbers` is now `[42]`
  // `twentyThree` is `23`
  
  // Remove the last element of the array.
  let fortyTwo = numbers.removeLast()
  // `numbers` is now `[]`
  // `fortyTwo` is `42`
  
  // Run-time error: The array is empty, the program aborts.
  numbers.removeLast()
  ```

## 字典

字典是键值关联的可变的、无序的集合。字典可能只包含一次键，也可能多次包含一个值。

字典文字以左大括号开始，以右大括号`{` 结束`}`。键与值之间用冒号分隔，键值关联用逗号分隔。

```cadence
// An empty dictionary
//
{}

// A dictionary which associates integers with booleans
//
{
    1: true,
    2: false
}
```

### 字典类型

字典类型的形式为`{K: V}`，其中`K`是键`V`的类型， 是值的类型。例如，带有`Int`键和`Bool` 值的字典的类型为`{Int: Bool}`。

在字典中，所有键的类型必须是字典的键类型 ( `K`) 的子类型，所有值的类型必须是字典的值类型 ( `V`)的子类型。

```cadence
// Declare a constant that has type `{Int: Bool}`,
// a dictionary mapping integers to booleans.
//
let booleans = {
    1: true,
    0: false
}

// Declare a constant that has type `{Bool: Int}`,
// a dictionary mapping booleans to integers.
//
let integers = {
    true: 1,
    false: 0
}

// Mixing keys with different types, and mixing values with different types,
// is possible by declaring the expected dictionary type with the common supertype
// of all keys, and the common supertype of all values.
//
let mixedValues: {String: AnyStruct} = {
    "a": 1,
    "b": true
}
```

字典类型的键和值类型是协变的。例如，`{Int: String}`是 的子类型，`{AnyStruct: String}` 也是 的子类型`{Int: AnyStruct}`。这是安全的，因为字典是值类型而不是引用类型。

### 字典访问

要从字典中获取特定键的值，可以使用访问语法：字典后跟一个左方括号`[`，键，并以右方括号结束`]`。

访问键返回一个[可选的](https://docs.onflow.org/cadence/language/values-and-types/#optionals)：如果在字典中找到键，则返回给定键的值，如果找不到键，`nil`则返回。

```cadence
// Declare a constant that has type `{Int: Bool}`,
// a dictionary mapping integers to booleans.
//
let booleans = {
    1: true,
    0: false
}

// The result of accessing a key has type `Bool?`.
//
booleans[1]  // is `true`
booleans[0]  // is `false`
booleans[2]  // is `nil`

// Invalid: Accessing a key which does not have type `Int`.
//
booleans["1"]
```

```cadence
// Declare a constant that has type `{Bool: Int}`,
// a dictionary mapping booleans to integers.
//
let integers = {
    true: 1,
    false: 0
}

// The result of accessing a key has type `Int?`
//
integers[true] // is `1`
integers[false] // is `0`
```

要设置字典键的值，也可以使用访问语法。

```cadence
// Declare a constant that has type `{Int: Bool}`,
// a dictionary mapping booleans to integers.
//
let booleans = {
    1: true,
    0: false
}

// Assign new values for the keys `1` and `0`.
//
booleans[1] = false
booleans[0] = true
// `booleans` is `{1: false, 0: true}`
```

### 字典字段和函数

- 

  ```cadence
  let length: Int
  ```

  

  字典中的条目数。

  ```cadence
  // Declare a dictionary mapping strings to integers.
  let numbers = {"fortyTwo": 42, "twentyThree": 23}
  
  // Find the number of entries of the dictionary.
  let length = numbers.length
  
  // `length` is `2`
  ```
  
- 

  ```cadence
  fun insert(key: K, _ value: V): V?
  ```

  

  将 type 的给定值插入到 type 给定下的`V`字典`key`中`K`。

  如果字典包含键，则返回前一个值作为可选值，否则返回`nil`。

  ```cadence
  // Declare a dictionary mapping strings to integers.
  let numbers = {"twentyThree": 23}
  
  // Insert the key `"fortyTwo"` with the value `42` into the dictionary.
  // The key did not previously exist in the dictionary,
  // so the result is `nil`
  //
  let old = numbers.insert(key: "fortyTwo", 42)
  
  // `old` is `nil`
  // `numbers` is `{"twentyThree": 23, "fortyTwo": 42}`
  ```
  
- 

  ```cadence
  fun remove(key: K): V?
  ```

  

  从字典中删除给定`key`类型的值`K`。

  `V`如果字典包含键，则返回 type 的值作为可选项，否则返回`nil`.

  ```cadence
  // Declare a dictionary mapping strings to integers.
  let numbers = {"fortyTwo": 42, "twentyThree": 23}
  
  // Remove the key `"fortyTwo"` from the dictionary.
  // The key exists in the dictionary,
  // so the value associated with the key is returned.
  //
  let fortyTwo = numbers.remove(key: "fortyTwo")
  
  // `fortyTwo` is `42`
  // `numbers` is `{"twentyThree": 23}`
  
  // Remove the key `"oneHundred"` from the dictionary.
  // The key does not exist in the dictionary, so `nil` is returned.
  //
  let oneHundred = numbers.remove(key: "oneHundred")
  
  // `oneHundred` is `nil`
  // `numbers` is `{"twentyThree": 23}`
  ```
  
- 

  ```cadence
  let keys: [K]
  ```

  

  返回`K`字典中类型键的数组。这不会修改字典，只是将键的副本作为数组返回。如果字典为空，则返回一个空数组。

  ```cadence
  // Declare a dictionary mapping strings to integers.
  let numbers = {"fortyTwo": 42, "twentyThree": 23}
  
  // Find the keys of the dictionary.
  let keys = numbers.keys
  
  // `keys` has type `[String]` and is `["fortyTwo","twentyThree"]`
  ```
  
- 

  ```cadence
  let values: [V]
  ```

  

  返回`V`字典中类型值的数组。这不会修改字典，只是将值的副本作为数组返回。如果字典为空，则返回一个空数组。

  如果`V`是资源类型，则此字段不可用。

  ```cadence
  // Declare a dictionary mapping strings to integers.
  let numbers = {"fortyTwo": 42, "twentyThree": 23}
  
  // Find the values of the dictionary.
  let values = numbers.values
  
  // `values` has type [Int] and is `[42, 23]`
  ```
  
- 

  ```cadence
  fun containsKey(key: K): Bool
  ```

  

  如果给定的 type 键`K`在字典中，则返回 true 。

  ```cadence
  // Declare a dictionary mapping strings to integers.
  let numbers = {"fortyTwo": 42, "twentyThree": 23}
  
  // Check if the dictionary contains the key "twentyFive".
  let containsKeyTwentyFive = numbers.containsKey("twentyFive")
  // `containsKeyTwentyFive` is `false`
  
  // Check if the dictionary contains the key "fortyTwo".
  let containsKeyFortyTwo = numbers.containsKey("fortyTwo")
  // `containsKeyFortyTwo` is `true`
  
  // Invalid: Check if the dictionary contains the key 42.
  // This results in a type error, as the key type of the dictionary is `String`.
  //
  let containsKey42 = numbers.containsKey(42)
  ```

### 字典键

字典键必须是可散列和可相等的，即必须实现[`Hashable`](https://docs.onflow.org/cadence/language/interfaces#hashable-interface) 和[接口](https://docs.onflow.org/cadence/language/interfaces)。[`Equatable`](https://docs.onflow.org/cadence/language/interfaces#equatable-interface)

大多数内置类型，如布尔值和整数，都是可散列和可相等的，因此可以用作字典中的键。