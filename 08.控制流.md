# 控制流

控制流语句控制函数中的执行流程。

## 条件分支：if 语句

If 语句只允许在给定条件为真时执行某段代码。

if 语句以`if`关键字开头，后跟条件，以及在左括号和右括号内条件为真时应执行的代码。条件表达式必须是布尔值。大括号是必需的，而不是可选的。条件周围的括号是可选的。

```cadence
let a = 0
var b = 0

if a == 0 {
   b = 1
}

// Parentheses can be used around the condition, but are not required.
if (a != 0) {
   b = 2
}

// `b` is `1`
```

当条件为假时，可以添加一个额外的、可选的 else 子句来执行另一段代码。else 子句由`else`关键字引入，后跟包含应该执行的代码的大括号。

```cadence
let a = 0
var b = 0

if a == 1 {
   b = 1
} else {
   b = 2
}

// `b` is `2`
```

else 子句可以包含另一个 if 语句，即 if 语句可以链接在一起。在这种情况下，可以省略大括号。

```cadence
let a = 0
var b = 0

if a == 1 {
   b = 1
} else if a == 2 {
   b = 2
} else {
   b = 3
}

// `b` is `3`

if a == 1 {
   b = 1
} else {
    if a == 0 {
        b = 2
    }
}

// `b` is `2`
```

## 可选绑定

可选绑定允许获取可选中的值。它是 if 语句的变体。

如果可选包含一个值，则执行第一个分支并声明一个临时常量或变量并将其设置为可选中包含的值；否则，执行 else 分支（如果有）。

可选绑定是使用`if`关键字（如 if 语句）声明的，但不是布尔测试值，而是后跟`let`or`var`关键字，以引入常量或变量，后跟名称、等号 ( `=`) 和可选值。

```cadence
let maybeNumber: Int? = 1

if let number = maybeNumber {
    // This branch is executed as `maybeNumber` is not `nil`.
    // The constant `number` is `1` and has type `Int`.
} else {
    // This branch is *not* executed as `maybeNumber` is not `nil`
}
```



```cadence
let noNumber: Int? = nil

if let number = noNumber {
    // This branch is *not* executed as `noNumber` is `nil`.
} else {
    // This branch is executed as `noNumber` is `nil`.
    // The constant `number` is *not* available.
}
```

## Switch

Switch 语句按顺序将一个值与多个相同类型的可能值进行比较。当找到相等的值时，将执行相关联的代码块。

switch 语句以`switch`关键字开始，然后是测试值，然后是左括号和右括号内的情况。测试表达式必须是相等的。大括号是必需的，而不是可选的。

每个 case 都是一个单独的代码执行分支，以`case`关键字开头，后跟一个可能的值、一个冒号 ( `:`)，以及如果 case 的值等于测试值时应该执行的代码块。

与 switch case 相关联的代码块 [不会隐式落入](https://docs.onflow.org/cadence/language/control-flow/#no-implicit-fallthrough)，并且必须至少包含一个语句。空块无效。

可以使用`default`关键字给出可选的默认情况。当之前的案例测试均未成功时，将执行默认案例的代码块。它必须总是最后出现。



```cadence
fun word(_ n: Int): String {
    // Test the value of the parameter `n`
    switch n {
    case 1:
        // If the value of variable `n` is equal to `1`,
        // then return the string "one"
        return "one"
    case 2:
        // If the value of variable `n` is equal to `2`,
        // then return the string "two"
        return "two"
    default:
        // If the value of variable `n` is neither equal to `1` nor to `2`,
        // then return the string "other"
        return "other"
    }
}

word(1)  // returns "one"
word(2)  // returns "two"
word(3)  // returns "other"
word(4)  // returns "other"
```

### 重复案例

案例按顺序进行测试，因此如果案例重复，则执行与第一个成功案例相关联的代码块。

复制

```cadence
fun test(_ n: Int): String {
    // Test the value of the parameter `n`
    switch n {
    case 1:
        // If the value of variable `n` is equal to `1`,
        // then return the string "one"
        return "one"
    case 1:
        // If the value of variable `n` is equal to `1`,
        // then return the string "also one".
        // This is a duplicate case for the one above.
        return "also one"
    default:
        // If the value of variable `n` is neither equal to `1` nor to `2`,
        // then return the string "other"
        return "other"
    }
}

word(1) // returns "one", not "also one"
```

### `break`

与 switch case 相关联的代码块可能包含一条`break`语句。它立即结束 switch 语句的执行，并将控制权转移到 switch 语句之后的代码

### 无隐式故障

与某些其他语言中的 switch 语句不同，Cadence 中的 switch 语句不会“失败”：只要与第一个匹配 case 相关联的代码块完成，switch 语句的执行就会结束。不需要明确的`break`声明。

这使得 switch 语句更安全、更易于使用，避免了多个 switch case 的意外执行。

其他一些语言隐式地落入与下一个 case 相关联的代码块，因此编写带有空块的 case 以相同的方式处理多个值是很常见的。

为了防止开发人员编写假设这种行为的 switch 语句，块必须至少有一个语句。空块无效。



```cadence
fun words(_ n: Int): [String] {
    // Declare a variable named `result`, an array of strings,
    // which stores the result
    let result: [String] = []

    // Test the value of the parameter `n`
    switch n {
    case 1:
        // If the value of variable `n` is equal to `1`,
        // then append the string "one" to the result array
        result.append("one")
    case 2:
        // If the value of variable `n` is equal to `2`,
        // then append the string "two" to the result array
        result.append("two")
    default:
        // If the value of variable `n` is neither equal to `1` nor to `2`,
        // then append the string "other" to the result array
        result.append("other")
    }
    return result
}

words(1)  // returns `["one"]`
words(2)  // returns `["two"]`
words(3)  // returns `["other"]`
words(4)  // returns `["other"]`
```

## 循环

### while 语句

While 语句允许重复执行某段代码，只要条件保持为真。

while 语句以`while`关键字开头，然后是条件，以及在左大括号和右大括号内条件为真时应重复执行的代码。条件必须是布尔值，并且需要大括号。

while 语句将首先评估条件。如果为真，则执行该代码段并重复条件的评估。如果条件为假，则不执行该段代码，并完成整个 while 语句的执行。因此，这段代码被执行零次或多次。



```cadence
var a = 0
while a < 5 {
    a = a + 1
}

// `a` is `5`
```

### for-in 语句

For-in 语句允许对数组中的每个元素重复执行一段代码。

for-in 语句以`for`关键字开头，后跟在循环的每次迭代中使用的元素的名称，然后是`in`关键字，然后是循环中正在迭代的数组。

然后，应该在循环的每次迭代中重复执行的代码用花括号括起来。

如果数据结构中没有元素，循环中的代码根本不会被执行。否则，代码将执行与数组中的元素一样多的次数。



```cadence
let array = ["Hello", "World", "Foo", "Bar"]

for element in array {
    log(element)
}

// The loop would log:
// "Hello"
// "World"
// "Foo"
// "Bar"
```

要迭代字典的条目（键和值），请在字典的键上使用 for-in 循环并获取每个键的值：



```cadence
let dictionary = {"one": 1, "two": 2}
for key in dictionary.keys {
    let value = dictionary[key]!
    log(key)
    log(value)
}

// The loop would log:
// "one"
// 1
// "two"
// 2
```

### `continue` 和 `break`

在 for 循环和 while 循环中，该`continue`语句可用于停止循环的当前迭代并开始下一次迭代。



```cadence
var i = 0
var x = 0
while i < 10 {
    i = i + 1
    if i < 3 {
        continue
    }
    x = x + 1
}
// `x` is `8`


let array = [2, 2, 3]
var sum = 0
for element in array {
    if element == 2 {
        continue
    }
    sum = sum + element
}

// `sum` is `3`
```

该`break`语句可用于停止 for 循环或 while 循环的执行。



```cadence
var x = 0
while x < 10 {
    x = x + 1
    if x == 5 {
        break
    }
}
// `x` is `5`


let array = [1, 2, 3]
var sum = 0
for element in array {
    if element == 2 {
        break
    }
    sum = sum + element
}

// `sum` is `1`
```

## 立即函数返回：return-statement

return 语句使函数立即返回，即不执行 return 语句之后的任何代码。return 语句以`return`关键字开头，后跟一个可选表达式，该表达式应该是函数调用的返回值。
