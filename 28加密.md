# 加密

------

## 散列

内置的 enum`HashAlgorithm`提供了该语言支持的一套散列算法。

 

```cadence
pub enum HashAlgorithm: UInt8 {
    /// SHA2_256 是具有 256 位摘要的安全散列算法 2 (SHA-2)
    pub case SHA2_256 = 1

    /// SHA2_384 是具有 384 位摘要的安全散列算法 2 (SHA-2)
    pub case SHA2_384 = 2

    /// SHA3_256 是具有 256 位摘要的安全散列算法 3 (SHA-3)
    pub case SHA3_256 = 3

    /// SHA3_384 是具有 384 位摘要的安全散列算法 3 (SHA-3)。
    pub case SHA3_384 = 4

    /// KMAC128_BLS_BLS12_381是KECCAK消息认证码（KMAC128）mac算法的一个实例，可以作为BLS12-381曲线上BLS签名方案的哈希算法。
	/// 这是KMAC128的定制版本，兼容用于 BLS 签名的hashing to curve
    pub case KMAC128_BLS_BLS12_381 = 5

    /// 返回给定数据的哈希值
    pub fun hash(_ data: [UInt8]): [UInt8]

    /// 返回给定数据和标签的哈希值
    pub fun hashWithTag(_ data: [UInt8], tag: string): [UInt8]
}
```

- `hash`使用输入哈希算法对输入数据进行哈希处理。如果`KMAC128_BLS_BLS12_381`使用，则使用`KMAC128(customizer, key, data, length)`具有以下输入的标准 MAC对数据进行散列：

  - `customizer` 是 UTF-8 编码 `"H2C"`
  - `key` 是 UTF-8 编码 `"APP_BLS_SIG_BLS12381G1_XOF:KMAC128_SSWU_RO_POP_"`
  - `data` 是要散列的输入数据
  - `length` 是 1024 字节

- `hashWithTag`散列数据和标签。这允许实例化使用域分离标签定制的独立散列函数。这取决于散列算法，实现方式不同：

  - `SHA2_256`，`SHA2_384`，`SHA3_256`，`SHA3_384`：散列消息是`bytes(tag) || data`其中`bytes()`是UTF-8编码的输入字符串的，用零填充，直到32个字节。接受的标签不得超过 32 个字节。如果使用的标签为空，则不应用数据前缀，散列消息只是`data`。

  - ```
    KMAC128_BLS_BLS12_381#这是提倡的标准
    ```

    ```
    KMAC128(customizer, key, data, length)
    ```
    
    具有以下输入的标准的调用：

    - `customizer` 是 UTF-8 编码 `"H2C"`
    - `key` 是 UTF-8 编码 `"APP_" || tag || "BLS_SIG_BLS12381G1_XOF:KMAC128_SSWU_RO_POP_"`
    - `data` 是要散列的输入数据
    - `length` 是 1024 字节

例如，要计算 SHA3-256 哈希：

```cadence
let data: [UInt8] = [1, 2, 3]
let digest = HashAlgorithm.SHA3_256.hash(data)
```

## 签名算法

内置的枚举`SignatureAlgorithm`提供了语言本身支持的一组签名算法。

```cadence
pub enum SignatureAlgorithm: UInt8 {
    /// ECDSA_P256 是 NIST P-256 曲线上的椭圆曲线数字签名算法 (ECDSA)。
    pub case ECDSA_P256 = 1

    /// ECDSA_secp256k1 是 secp256k1 曲线上的椭圆曲线数字签名算法 (ECDSA)。
    pub case ECDSA_secp256k1 = 2

    /// BLS_BLS12_381是BLS12-381曲线上的BLS签名方案。
	/// 该方案已设置，以便当公钥在 G_2 中时（主要字段扩展上的曲线）。签名也在 G_1 中（质数字段上的曲线）
	/// 
    pub case BLS_BLS12_381 = 3
}
```

## 公钥

`PublicKey` 是表示签名方案的加密公钥的内置结构。

 

```cadence
struct PublicKey {
    let publicKey: [UInt8]
    let signatureAlgorithm: SignatureAlgorithm
    let isValid: Bool

    /// 验证给定标签、数据和公钥下的签名。
	/// 使用给定的哈希算法对标签和数据进行哈希处理。
    pub fun verify(
        signature: [UInt8],
        signedData: [UInt8],
        domainSeparationTag: String,
        hashAlgorithm: HashAlgorithm
    ): Bool
}
```

A`PublicKey`可以使用原始密钥和签名算法构造。

```cadence
let publicKey = PublicKey(
    publicKey: "010203".decodeHex(),
    signatureAlgorithm: SignatureAlgorithm.ECDSA_P256
)
```

原始密钥值取决于支持的签名方案：

- `ECDSA_P256`and `ECDSA_secp256k1`：公钥是一个曲线点`(X,Y)`，其中`X`和`Y`是两个素数域元素。原始键表示为`bytes(X) || bytes(Y)`，其中`||`是连接操作，`bytes()`是输入整数的字节大端编码，由零填充到字段素数的字节长度。原始公钥是 64 字节长。
- `BLS_BLS_12_381`：公钥是一个 G_2（质数域扩展上的曲线）元素。编码遵循[IETF draft-irtf-cfrg-pairing-friendly-curves-08 中](https://www.ietf.org/archive/id/draft-irtf-cfrg-pairing-friendly-curves-08.html#name-point-serialization-procedu)定义的压缩序列化 。公钥是 96 字节长。

公钥在创建时进行验证。因此，公钥是不可变的。

```cadence
publicKey.signatureAlgorithm = SignatureAlgorithm.ECDSA_secp256k1   // Not allowed
publicKey.publicKey = []                                            // Not allowed

publicKey.publicKey[2] = 4      // No effect
```

可以使用该`isValid`字段检查公钥的有效性。使用无效公钥（使用`verify()`方法）执行的验证将始终失败。

### 签名验证

可以使用以下`verify`函数验证签名`PublicKey`：

```cadence
let pk = PublicKey(
    publicKey: "96142CE0C5ECD869DC88C8960E286AF1CE1B29F329BA4964213934731E65A1DE480FD43EF123B9633F0A90434C6ACE0A98BB9A999231DB3F477F9D3623A6A4ED".decodeHex(),
    signatureAlgorithm: SignatureAlgorithm.ECDSA_P256
)

let signature = "108EF718F153CFDC516D8040ABF2C8CC7AECF37C6F6EF357C31DFE1F7AC79C9D0145D1A2F08A48F1A2489A84C725D6A7AB3E842D9DC5F8FE8E659FFF5982310D".decodeHex()
let message : [UInt8] = [1, 2, 3]

let isValid = pk.verify(
    signature: signature,
    signedData: message,
    domainSeparationTag: "",
    hashAlgorithm: HashAlgorithm.SHA2_256
)
// `isValid` is 错误
```

`verify()`取决于所使用的签名方案的输入：

- ECDSA（`ECDSA_P256`和`ECDSA_secp256k1`）：
  - `signature`是夫妻`(r,s)`。它表示为`bytes(r) || bytes(s)`，其中`||`是连接操作，`bytes()`是输入整数的字节大端编码，由零填充到曲线顺序的字节长度。两条曲线的签名都是 64 字节长。
  - `signedData` 是用于验证签名的任意消息。
  - `domainSeparationTag`是用于签名的域标签。接口只接受用户标签`"FLOW-V0.0-user"`。
  - `hashAlgorithm`是用于将消息与给定标签一起散列的算法（查看[`hashWithTag`函数](https://docs.onflow.org/cadence/language/crypto/#hashing)以获取更多详细信息）。只有`SHA2_256`或被`SHA3_256`接受。
- BLS ( `BLS_BLS_12_381`):
  - `signature`是 G_1（素域上的曲线）点。编码遵循[IETF draft-irtf-cfrg-pairing-friendly-curves-08 中](https://www.ietf.org/archive/id/draft-irtf-cfrg-pairing-friendly-curves-08.html#name-point-serialization-procedu)定义的压缩序列化。签名是 48 字节长。
  - `signedData` 是用于验证签名的任意消息。
  - `domainSeparationTag`是用于签名的域标签。接受所有标签。
  - `hashAlgorithm`是用于将消息与给定标签一起散列的算法（`hashWithTag`有关更多详细信息，请查看函数）。只`KMAC128_BLS_BLS12_381`接受。

BLS 验证执行必要的签名成员资格检查，而公钥的成员资格检查在创建`PublicKey`对象时执行。

## 加密合约

内置合约`Crypto`可用于执行加密操作。可以使用 导入合同`import Crypto`。

### 关键列表

加密合约还允许创建用于多重签名验证的密钥列表。例如，要验证某些签名数据具有相同权重的两个签名：

```cadence
import Crypto

pub fun test main() {
    let keyList = Crypto.KeyList()

    let publicKeyA = PublicKey(
        publicKey:
            "db04940e18ec414664ccfd31d5d2d4ece3985acb8cb17a2025b2f1673427267968e52e2bbf3599059649d4b2cce98fdb8a3048e68abf5abe3e710129e90696ca".decodeHex(),
        signatureAlgorithm: SignatureAlgorithm.ECDSA_P256
    )
    keyList.add(
        publicKeyA,
        hashAlgorithm: HashAlgorithm.SHA3_256,
        weight: 0.5
    )

    let publicKeyB = PublicKey(
        publicKey:
            "df9609ee588dd4a6f7789df8d56f03f545d4516f0c99b200d73b9a3afafc14de5d21a4fc7a2a2015719dc95c9e756cfa44f2a445151aaf42479e7120d83df956".decodeHex(),
        signatureAlgorithm: SignatureAlgorithm.ECDSA_P256
    )
    keyList.add(
        publicKeyB,
        hashAlgorithm: HashAlgorithm.SHA3_256,
        weight: 0.5
    )

    let signatureSet = [
        Crypto.KeyListSignature(
            keyIndex: 0,
            signature:
                "8870a8cbe6f44932ba59e0d15a706214cc4ad2538deb12c0cf718d86f32c47765462a92ce2da15d4a29eb4e2b6fa05d08c7db5d5b2a2cd8c2cb98ded73da31f6".decodeHex()
        ),
        Crypto.KeyListSignature(
            keyIndex: 1,
            signature:
                "bbdc5591c3f937a730d4f6c0a6fde61a0a6ceaa531ccb367c3559335ab9734f4f2b9da8adbe371f1f7da913b5a3fdd96a871e04f078928ca89a83d841c72fadf".decodeHex()
        )
    ]

    // “foo”，编码为 UTF-8，十六进制表示
    let signedData = "666f6f".decodeHex()

    let isValid = keyList.verify(
        signatureSet: signatureSet,
        signedData: signedData
    )
}
```

与密钥列表相关的加密合约的 API 是：

```cadence
pub struct KeyListEntry {
    pub let keyIndex: Int
    pub let publicKey: PublicKey
    pub let hashAlgorithm: HashAlgorithm
    pub let weight: UFix64
    pub let isRevoked: Bool

    init(
        keyIndex: Int,
        publicKey: PublicKey,
        hashAlgorithm: HashAlgorithm,
        weight: UFix64,
        isRevoked: Bool
    )
}

pub struct KeyList {

    init()

    /// 添加具有给定权重的新键
    pub fun add(
        _ publicKey: PublicKey,
        hashAlgorithm: HashAlgorithm,
        weight: UFix64
    )

    /// 返回给定索引处的键（如果存在）。
	/// 撤销的键总是被返回，但它们的 `isRevoked` 字段设置为 true
    pub fun get(keyIndex: Int): KeyListEntry?

    /// 将给定索引处的键标记为已撤销，但不删除它
    pub fun revoke(keyIndex: Int)

    /// 如果给定的签名对于给定的签名数据有效，则返回 true，反之，返回flase
    pub fun verify(
        signatureSet: [KeyListSignature],
        signedData: [UInt8]
    ): Bool
}

pub struct KeyListSignature {
    pub let keyIndex: Int
    pub let signature: [UInt8]

    pub init(keyIndex: Int, signature: [UInt8])
}
```