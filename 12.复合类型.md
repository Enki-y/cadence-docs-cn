# 复合类型

------

复合类型允许将更简单的类型组合成更复杂的类型，即它们允许将多个值组合为一个。复合类型有一个名称，由零个或多个命名字段以及零个或多个对数据进行操作的函数组成。每个字段可能有不同的类型。

复合类型只能在[合同中](https://docs.onflow.org/cadence/language/contracts)声明，而不能在其他地方声明。

有两种复合类型。当一个值用作常量或变量的初始值时，当值被分配给变量时，当值作为参数传递给函数时，以及当值是从函数返回：

- [**结构**](https://docs.onflow.org/cadence/language/composite-types/#structures)被**复制**，它们是值类型。

  当需要具有独立状态的副本时，结构很有用。

- [**资源**](https://docs.onflow.org/cadence/language/composite-types/#resources)被**移动**，它们是线性类型并且**必须****恰好**使用**一次**。

  当需要对所有权进行建模时，资源很有用（值恰好存在于一个位置并且不应丢失）。

  区块链中的某些构造代表具有真实、有形价值的资产，就像房子、汽车或银行账户一样。我们不得不担心字面上的损失和盗窃，甚至可能达到数百万美元的规模。

  结构不是表示这种所有权的理想方式，因为它们是被复制的。这意味着可能存在某些资产的多个副本四处流动的风险，这打破了这些资产具有实际价值所需的稀缺性要求。

  结构对于表示可以以逻辑方式组合在一起但没有价值或需要能够被拥有或转移的信息更有用。

  例如，结构可用于包含与公司部门相关的信息，但资源可用于表示已分配给该组织用于支出的资产。

资源的嵌套只允许在其他资源类型中，或在数组和字典等数据结构中，但不允许在结构中，因为这将允许复制资源。

## 复合类型声明和创建

使用`struct`关键字声明结构，使用关键字声明资源`resource`。关键字后跟名称。

```cadence
pub struct SomeStruct {
    // ...
}

pub resource SomeResource {
    // ...
}
```

结构和资源是类型。

通过像函数一样调用类型来创建（实例化）结构。

```cadence
// instantiate a new struct object and assign it to a constant
let a = SomeStruct()
```

如果 复合类型的[初始化程序](https://docs.onflow.org/cadence/language/composite-types/#composite-type-fields)需要参数，则构造函数可能需要参数。

复合类型只能在[契约中](https://docs.onflow.org/cadence/language/contracts)声明， 而不能在函数中本地声明。

必须通过使用`create`关键字并像函数一样调用类型来创建（实例化）资源。

资源只能在声明资源的同一合约中声明的函数和类型中创建。

```cadence
// instantiate a new resource object and assign it to a constant
let b <- create SomeResource()
```

## 复合类型字段

字段被声明为变量和常量。但是，字段的初始值是在初始化程序中设置的， **而不是**在字段声明中设置的。所有字段都**必须**在初始化程序中初始化一次。

必须在初始化程序中提供初始值似乎受到限制，但这可确保所有字段始终在一个位置初始化，即初始化程序，并且初始化顺序是明确的。

静态检查所有字段的初始化，不初始化初始化器中的所有字段是无效的。此外，静态检查字段在使用前是否已明确初始化。

初始化程序的主要目的是初始化字段，尽管它也可能包含其他代码。就像函数一样，它可以声明参数并且可以包含任意代码。但是，它没有返回类型，即始终为`Void`。

初始化器是使用`init`关键字声明的。

初始值设定项始终跟在任何字段之后。

共有三种字段：

- **常量字段**也存储在复合值中，但在用值初始化后，它们**不能**再分配新值。一个常量字段必须被初始化一次。

  常量字段使用`let`关键字声明。

- **变量字段**存储在复合值中，并且可以为其分配新值。

  变量字段使用`var`关键字声明。

- **合成领域**中**没有存储**在复合值，即，它们是衍生的/从其他值来计算。可以为它们分配新值。

  合成字段使用`synthetic`关键字声明。

  合成字段必须有一个 getter 和一个 setter。[下一节](https://docs.onflow.org/cadence/language/composite-types/#composite-type-field-getters-and-setters)将解释 getter 和 setter 。合成字段在[单独的部分](https://docs.onflow.org/cadence/language/composite-types/#synthetic-composite-type-fields)中解释。

| 字段种类     | 存储在内存中 | 可分配 | 关键词      |
| ------------ | ------------ | ------ | ----------- |
| **变量字段** | 是的         | 是的   | `var`       |
| **常数场**   | 是的         | **不** | `let`       |
| **合成场**   | **不**       | 是的   | `synthetic` |

在初始化器中，特殊常量`self`指的是要初始化的复合值。

字段类型必须是可存储的。不可存储的类型有：

- 职能
- [帐户 ( `AuthAccount`/ `PublicAccount`)](https://docs.onflow.org/cadence/language/accounts)
- [交易](https://docs.onflow.org/cadence/language/transactions)
- [参考资料](https://docs.onflow.org/cadence/language/references)：参考资料是短暂的。考虑[存储能力并](https://docs.onflow.org/cadence/language/capability-based-access-control)在需要时[借用它](https://docs.onflow.org/cadence/language/capability-based-access-control)。

可以使用访问语法读取（如果它们是常量或可变的）和设置（如果它们是可变的）`.`字段：复合值后跟一个点 ( ) 和字段的名称。

```cadence
// Declare a structure named `Token`, which has a constant field
// named `id` and a variable field named `balance`.
//
// Both fields are initialized through the initializer.
//
// The public access modifier `pub` is used in this example to allow
// the fields to be read in outer scopes. Fields can also be declared
// private so they cannot be accessed in outer scopes.
// Access control will be explained in a later section.
//
pub struct Token {
    pub let id: Int
    pub var balance: Int

    init(id: Int, balance: Int) {
        self.id = id
        self.balance = balance
    }
}
```

请注意，在字段声明中为字段提供初始值是无效的。

```cadence
pub struct StructureWithConstantField {
    // Invalid: It is invalid to provide an initial value in the field declaration.
    // The field must be initialized by setting the initial value in the initializer.
    //
    pub let id: Int = 1
}
```

必须使用字段访问语法来访问字段——字段不能作为变量使用。

```cadence
pub struct Token {
    pub let id: Int

    init(initialID: Int) {
        // Invalid: There is no variable with the name `id` available.
        // The field `id` must be initialized by setting `self.id`.
        //
        id = initialID
    }
}
```

初始值设定项**不是**从字段自动派生的，它必须显式声明。

```cadence
pub struct Token {
    pub let id: Int

    // Invalid: Missing initializer initializing field `id`.
}
```

可以通过调用构造函数并提供字段值作为参数来创建复合值。

创建对象后，可以在对象上访问值的字段。

```cadence
let token = Token(id: 42, balance: 1_000_00)

token.id  // is `42`
token.balance  // is `1_000_000`

token.balance = 1
// `token.balance` is `1`

// Invalid: assignment to constant field
//
token.id = 23
```

## 资源所有者

资源具有隐式字段`let owner: PublicAccount?`。如果资源当前[存储在 account 中](https://docs.onflow.org/cadence/language/accounts#account-storage)，则该字段包含该帐户的可公开访问部分。否则该字段为`nil`。

当资源从外部账户存储移动到账户存储时，当它从一个账户的存储移动到另一个账户的存储时，以及当它从账户存储中移出时，该字段的值会发生变化。

## 复合数据初始化器重载

🚧 状态：初始化器重载尚未实现。

初始化程序支持重载。这允许例如为某些参数提供默认值。

```cadence
// Declare a structure named `Token`, which has a constant field
// named `id` and a variable field named `balance`.
//
// The first initializer allows initializing both fields with a given value.
//
// A second initializer is provided for convenience to initialize the `id` field
// with a given value, and the `balance` field with the default value `0`.
//
pub struct Token {
    let id: Int
    var balance: Int

    init(id: Int, balance: Int) {
        self.id = id
        self.balance = balance
    }

    init(id: Int) {
        self.id = id
        self.balance = 0
    }
}
```

## 复合类型字段 Getter 和 Setter

🚧 状态：字段 getter 和 setter 尚未实现。

字段可能有一个可选的 getter 和一个可选的 setter。getter 是读取字段时调用的函数，setter 是写入字段时调用的函数。在 getter 和 setter 中只允许某些赋值。

在字段类型之后，getter 和 setter 用左括号和右括号括起来。

Getter 是使用`get`关键字声明的。Getter 没有参数，它们的返回类型隐式是字段的类型。

```cadence
pub struct GetterExample {

    // Declare a variable field named `balance` with a getter
    // which ensures the read value is always non-negative.
    //
    pub var balance: Int {
        get {
           if self.balance < 0 {
               return 0
           }

           return self.balance
        }
    }

    init(balance: Int) {
        self.balance = balance
    }
}

let example = GetterExample(balance: 10)
// `example.balance` is `10`

example.balance = -50
// The stored value of the field `example` is `-50` internally,
// though `example.balance` is `0` because the getter for `balance` returns `0` instead.
```

Setter 使用`set`关键字声明，后跟括在括号中的新值的名称。该参数隐含了字段的类型。不能指定其他类型。Setter 没有返回类型。

分配给 setter 的值的类型必须始终与字段的类型匹配。

```cadence
pub struct SetterExample {

    // Declare a variable field named `balance` with a setter
    // which requires written values to be positive.
    //
    pub var balance: Int {
        set(newBalance) {
            pre {
                newBalance >= 0
            }
            self.balance = newBalance
        }
    }

    init(balance: Int) {
        self.balance = balance
    }
}

let example = SetterExample(balance: 10)
// `example.balance` is `10`

// Run-time error: The precondition of the setter for the field `balance` fails,
// the program aborts.
//
example.balance = -50
```

## 合成复合类型字段

🚧 状态：合成字段尚未实现。

未存储在复合值中的字段是*合成的*，即计算字段值。Synthetic 可以是只读的，也可以是可读可写的。

合成字段使用`synthetic`关键字声明。

当仅提供 getter 时，合成字段是只读的。

```cadence
struct Rectangle {
    pub var width: Int
    pub var height: Int

    // Declare a synthetic field named `area`,
    // which computes the area based on the `width` and `height` fields.
    //
    pub synthetic area: Int {
        get {
            return width * height
        }
    }

    // Declare an initializer which accepts width and height.
    // As `area` is synthetic and there is only a getter provided for it,
    // the `area` field cannot be assigned a value.
    //
    init(width: Int, height: Int) {
        self.width = width
        self.height = height
    }
}
```

当同时声明了 getter 和 setter 时，合成字段是可读和可写的。

```cadence
// Declare a struct named `GoalTracker` which stores a number
// of target goals, a number of completed goals,
// and has a synthetic field to provide the left number of goals.
//
// NOTE: the tracker only implements some functionality to demonstrate
// synthetic fields, it is incomplete (e.g. assignments to `goal` are not handled properly).
//
pub struct GoalTracker {

    pub var goal: Int
    pub var completed: Int

    // Declare a synthetic field which is both readable and writable.
    //
    // When the field is read from (in the getter), the number
    // of left goals is computed from the target number of goals
    // and the completed number of goals.
    //
    // When the field is written to (in the setter), the number
    // of completed goals is updated, based on the number
    // of target goals and the new remaining number of goals.
    //
    pub synthetic left: Int {
        get {
            return self.goal - self.completed
        }

        set(newLeft) {
            self.completed = self.goal - newLeft
        }
    }

    init(goal: Int, completed: Int) {
        self.goal = goal
        self.completed = completed
    }
}

let tracker = GoalTracker(goal: 10, completed: 0)
// `tracker.goal` is `10`
// `tracker.completed` is `0`
// `tracker.left` is `10`

tracker.completed = 1
// `tracker.left` is `9`

tracker.left = 8
// `tracker.completed` is `2`
```

仅使用 setter 声明合成字段是无效的。

## 复合类型函数

🚧 状态：函数重载还没有实现。

复合类型可能包含函数。就像在初始化程序中一样，特殊常量`self`指的是调用函数的复合值。

```cadence
// Declare a structure named "Rectangle", which represents a rectangle
// and has variable fields for the width and height.
//
pub struct Rectangle {
    pub var width: Int
    pub var height: Int

    init(width: Int, height: Int) {
        self.width = width
        self.height = height
    }

    // Declare a function named "scale", which scales
    // the rectangle by the given factor.
    //
    pub fun scale(factor: Int) {
        self.width = self.width * factor
        self.height = self.height * factor
    }
}

let rectangle = Rectangle(width: 2, height: 3)
rectangle.scale(factor: 4)
// `rectangle.width` is `8`
// `rectangle.height` is `12`
```

函数支持重载。

```cadence
// Declare a structure named "Rectangle", which represents a rectangle
// and has variable fields for the width and height.
//
pub struct Rectangle {
    pub var width: Int
    pub var height: Int

    init(width: Int, height: Int) {
        self.width = width
        self.height = height
    }

    // Declare a function named "scale", which independently scales
    // the width by a given factor and the height by a given factor.
    //
    pub fun scale(widthFactor: Int, heightFactor: Int) {
        self.width = self.width * widthFactor
        self.height = self.height * heightFactor
    }

    // Declare another function also named "scale", which scales
    // both width and height by a given factor.
    // The function calls the `scale` function declared above.
    //
    pub fun scale(factor: Int) {
        self.scale(
            widthFactor: factor,
            heightFactor: factor
        )
    }
}
```

## 复合类型子类型

两个复合类型是兼容的，当且仅当它们通过名称引用相同的声明，即应用名义类型而不是结构类型。

即使两个复合类型声明了相同的字段和函数，这些类型也只有在名称匹配时才兼容。

```cadence
// Declare a structure named `A` which has a function `test`
// which has type `((): Void)`.
//
struct A {
    fun test() {}
}

// Declare a structure named `B` which has a function `test`
// which has type `((): Void)`.
//
struct B {
    fun test() {}
}

// Declare a variable named which accepts values of type `A`.
//
var something: A = A()

// Invalid: Assign a value of type `B` to the variable.
// Even though types `A` and `B` have the same declarations,
// a function with the same name and type, the types' names differ,
// so they are not compatible.
//
something = B()

// Valid: Reassign a new value of type `A`.
//
something = A()
```

## 复合类型行为

### 结构

当用作常量或变量的初始值、分配给不同的变量、作为参数传递给函数以及从函数返回时，结构将被**复制**。

访问字段或调用结构的函数不会复制它。

```cadence
// Declare a structure named `SomeStruct`, with a variable integer field.
//
pub struct SomeStruct {
    pub var value: Int

    init(value: Int) {
        self.value = value
    }

    fun increment() {
        self.value = self.value + 1
    }
}

// Declare a constant with value of structure type `SomeStruct`.
//
let a = SomeStruct(value: 0)

// *Copy* the structure value into a new constant.
//
let b = a

b.value = 1
// NOTE: `b.value` is 1, `a.value` is *`0`*

b.increment()
// `b.value` is 2, `a.value` is `0`
```

### 使用可选链访问复合类型的字段和函数

如果具有字段和函数的复合类型被包装在一个可选项中，则可以使用可选项链来获取这些值或调用函数，而不必先获取可选项的值。

可选链是通过`?` 在`.`可选复合类型的字段或函数的访问运算符之前添加 a 来使用的。

当获取字段值或调用具有返回值的函数时，访问会将该值作为可选项返回。如果对象不存在，则值将始终为`nil`

当在这样的可选项上调用函数时，如果对象不存在，则什么都不会发生，执行将继续。

访问可选复合类型的未声明字段仍然无效。

```cadence
// Declare a struct with a field and method.
pub struct Value {
    pub var number: Int

    init() {
        self.number = 2
    }

    pub fun set(new: Int) {
        self.number = new
    }

    pub fun setAndReturn(new: Int): Int {
        self.number = new
        return new
    }
}

// Create a new instance of the struct as an optional
let value: Value? = Value()
// Create another optional with the same type, but nil
let noValue: Value? = nil

// Access the `number` field using optional chaining
let twoOpt = value?.number
// Because `value` is an optional, `twoOpt` has type `Int?`
let two = twoOpt ?? 0
// `two` is `2`

// Try to access the `number` field of `noValue`, which has type `Value?`.
// This still returns an `Int?`
let nilValue = noValue?.number
// This time, since `noValue` is `nil`, `nilValue` will also be `nil`

// Try to call the `set` function of `value`.
// The function call is performed, as `value` is not nil
value?.set(new: 4)

// Try to call the `set` function of `noValue`.
// The function call is *not* performed, as `noValue` is nil
noValue?.set(new: 4)

// Call the `setAndReturn` function, which returns an `Int`.
// Because `value` is an optional, the return value is type `Int?`
let sixOpt = value?.setAndReturn(new: 6)
let six = sixOpt ?? 0
// `six` is `6`
```

这也可以通过使用强制展开运算符 ( `!`) 来实现。

通过`!` 在`.`可选复合类型的字段或函数的访问运算符之前添加 a 来使用强制可选链接。

当获取字段值或调用带有返回值的函数时，访问会返回该值。如果对象不存在，则执行将恐慌并恢复。

访问可选复合类型的未声明字段仍然无效。

```cadence
// Declare a struct with a field and method.
pub struct Value {
    pub var number: Int

    init() {
        self.number = 2
    }

    pub fun set(new: Int) {
        self.number = new
    }

    pub fun setAndReturn(new: Int): Int {
        self.number = new
        return new
    }
}

// Create a new instance of the struct as an optional
let value: Value? = Value()
// Create another optional with the same type, but nil
let noValue: Value? = nil

// Access the `number` field using force-optional chaining
let two = value!.number
// `two` is `2`

// Try to access the `number` field of `noValue`, which has type `Value?`
// Run-time error: This time, since `noValue` is `nil`,
// the program execution will revert
let number = noValue!.number

// Call the `set` function of the struct

// This succeeds and sets the value to 4
value!.set(new: 4)

// Run-time error: Since `noValue` is nil, the value is not set
// and the program execution reverts.
noValue!.set(new: 4)

// Call the `setAndReturn` function, which returns an `Int`
// Because we use force-unwrap before calling the function,
// the return value is type `Int`
let six = value!.setAndReturn(new: 6)
// `six` is `6`
```

### 资源

资源是一次只能存在于**一个**位置并且**必须****恰好**使用**一次的类型**。

**必须**使用`create`关键字创建（实例化）资源。

在具有在范围资源（变量，常量参数）的函数的结束时，资源**必须**被任一**移动**或**破坏**。

当用作常量或变量的初始值、分配给不同的变量、作为参数传递给函数以及从函数返回时，它们都会被**移动**。

可以使用关键字显式**销毁**资源`destroy`。

访问字段或调用资源的函数不会移动或破坏它。

移动资源时，移动前引用资源的常量或变量将变为**无效**。一个**无效的**资源不能再使用。

为了明确资源类型的用法和行为，`@`必须在变量或常量声明、参数和返回类型的类型注释中使用前缀。

为了明确地移动资源，`<-`当资源是常量或变量的初始值时，当它被移动到一个不同的变量时，当它被移动到一个函数作为参数时，以及当它是从函数返回。



```cadence
// Declare a resource named `SomeResource`, with a variable integer field.
//
pub resource SomeResource {
    pub var value: Int

    init(value: Int) {
        self.value = value
    }
}

// Declare a constant with value of resource type `SomeResource`.
//
let a: @SomeResource <- create SomeResource(value: 0)

// *Move* the resource value to a new constant.
//
let b <- a

// Invalid: Cannot use constant `a` anymore as the resource that it referred to
// was moved to constant `b`.
//
a.value

// Constant `b` owns the resource.
//
b.value // equals 0

// Declare a function which accepts a resource.
//
// The parameter has a resource type, so the type annotation must be prefixed with `@`.
//
pub fun use(resource: @SomeResource) {
    // ...
}

// Call function `use` and move the resource into it.
//
use(resource: <-b)

// Invalid: Cannot use constant `b` anymore as the resource
// it referred to was moved into function `use`.
//
b.value
```

资源对象不能超出范围并动态丢失。程序必须显式销毁它或将其移动到另一个上下文。

```cadence
{
    // Declare another, unrelated value of resource type `SomeResource`.
    //
    let c <- create SomeResource(value: 10)

    // Invalid: `c` is not used before the end of the scope, but must be.
    // It cannot be lost.
}
```

```cadence
// Declare another, unrelated value of resource type `SomeResource`.
//
let d <- create SomeResource(value: 20)

// Destroy the resource referred to by constant `d`.
//
destroy d

// Invalid: Cannot use constant `d` anymore as the resource
// it referred to was destroyed.
//
d.value
```

为了明确该类型是一种资源类型并且必须遵循与资源相关的规则，它必须`@`在所有类型注释中加上前缀，例如对于变量声明、参数或返回类型。

```cadence
// Declare a constant with an explicit type annotation.
//
// The constant has a resource type, so the type annotation must be prefixed with `@`.
//
let someResource: @SomeResource <- create SomeResource(value: 5)

// Declare a function which consumes a resource and destroys it.
//
// The parameter has a resource type, so the type annotation must be prefixed with `@`.
//
pub fun use(resource: @SomeResource) {
    destroy resource
}

// Declare a function which returns a resource.
//
// The return type is a resource type, so the type annotation must be prefixed with `@`.
// The return statement must also use the `<-` operator to make it explicit the resource is moved.
//
pub fun get(): @SomeResource {
    let newResource <- create SomeResource()
    return <-newResource
}
```

资源**必须**恰好使用一次。

```cadence
// Declare a function which consumes a resource but does not use it.
// This function is invalid, because it would cause a loss of the resource.
//
pub fun forgetToUse(resource: @SomeResource) {
    // Invalid: The resource parameter `resource` is not used, but must be.
}
```

```cadence
// Declare a constant named `res` which has the resource type `SomeResource`.
let res <- create SomeResource()

// Call the function `use` and move the resource `res` into it.
use(resource: <-res)

// Invalid: The resource constant `res` cannot be used again,
// as it was moved in the previous function call.
//
use(resource: <-res)

// Invalid: The resource constant `res` cannot be used again,
// as it was moved in the previous function call.
//
res.value
```

```cadence
// Declare a function which has a resource parameter.
// This function is invalid, because it does not always use the resource parameter,
// which would cause a loss of the resource.
//
pub fun sometimesDestroy(resource: @SomeResource, destroyResource: Bool) {
    if destroyResource {
        destroy resource
    }
    // Invalid: The resource parameter `resource` is not always used, but must be.
    // The destroy statement is not always executed, so at the end of this function
    // it might have been destroyed or not.
}
```

```cadence
// Declare a function which has a resource parameter.
// This function is valid, as it always uses the resource parameter,
// and does not cause a loss of the resource.
//
pub fun alwaysUse(resource: @SomeResource, destroyResource: Bool) {
    if destroyResource {
        destroy resource
    } else {
        use(resource: <-resource)
    }
    // At the end of the function the resource parameter was definitely used:
    // It was either destroyed or moved in the call of function `use`.
}
```

```cadence
// Declare a function which has a resource parameter.
// This function is invalid, because it does not always use the resource parameter,
// which would cause a loss of the resource.
//
pub fun returnBeforeDestroy(move: Bool) {
    let res <- create SomeResource(value: 1)
    if move {
        use(resource: <-res)
        return
    } else {
        // Invalid: When this function returns here, the resource variable
        // `res` was not used, but must be.
        return
    }
    // Invalid: the resource variable `res` was potentially moved in the
    // previous if-statement, and both branches definitely return,
    // so this statement is unreachable.
    destroy res
}
```

### 资源变量

不能分配资源变量，因为这会导致变量当前资源值的丢失。

相反，使用 swap 语句 ( `<->`) 或 shift 语句 ( `<- target <-`) 将资源变量替换为另一个资源。

```cadence
pub resource R {}

var x <- create R()
var y <- create R()

// Invalid: Cannot assign to resource variable `x`,
// as its current resource would be lost
//
x <- y

// Instead, use a swap statement.
//
var replacement <- create R()
x <-> replacement
// `x` is the new resource.
// `replacement` is the old resource.

// Or use the shift statement (`<- target <-`)
// This statement moves the resource out of `x` and into `oldX`,
// and at the same time assigns `x` with the new value on the right-hand side.
let oldX <- x <- create R()
// oldX still needs to be explicitly handled after this statement
destroy oldX
```

### 资源析构函数

资源可能有一个析构函数，它在资源被销毁时执行。析构函数没有参数和返回值，并使用`destroy`名称声明。一个资源可能只有一个析构函数。

```cadence
var destructorCalled = false

pub resource Resource {

    // Declare a destructor for the resource, which is executed
    // when the resource is destroyed.
    //
    destroy() {
        destructorCalled = true
    }
}

let res <- create Resource()
destroy res
// `destructorCalled` is `true`
```

### 嵌套资源

当复合类型中的字段具有资源类型时，它们的行为会有所不同。

如果资源类型具有具有资源类型的字段，则它**必须**声明一个析构函数，该析构函数**必须**使所有资源字段无效，即移动或销毁它们。

```cadence
pub resource Child {
    let name: String

    init(name: String)
        self.name = name
    }
}

// Declare a resource with a resource field named `child`.
// The resource *must* declare a destructor
// and the destructor *must* invalidate the resource field.
//
pub resource Parent {
    let name: String
    var child: @Child

    init(name: String, child: @Child) {
        self.name = name
        self.child <- child
    }

    // Declare a destructor which invalidates the resource field
    // `child` by destroying it.
    //
    destroy() {
        destroy self.child
    }
}
```

在资源领域的访问字段或调用函数是有效的，但是移动的资源，变量资源领域是**不是**允许。相反，使用交换语句将资源替换为另一个资源。

```cadence
let child <- create Child(name: "Child 1")
let parent <- create Parent(name: "Parent", child: <-child)

child.name  // is "Child 1"
parent.child.name  // is "Child 1"

// Invalid: Cannot move resource out of variable resource field.
let childAgain <- parent.child

// Instead, use a swap statement.
//
var otherChild <- create Child(name: "Child 2")
parent.child <-> otherChild
// `parent.child` is the second child, Child 2.
// `otherChild` is the first child, Child 1.
```

### 闭包中的资源

资源不能在闭包中捕获，因为这可能会导致重复。

```cadence
resource R {}

// Invalid: Declare a function which returns a closure which refers to
// the resource parameter `resource`. Each call to the returned function
// would return the resource, which should not be possible.
//
fun makeCloner(resource: @R): ((): @R) {
    return fun (): @R {
        return <-resource
    }
}

let test = makeCloner(resource: <-create R())
```

### 数组和字典中的资源

数组和字典的行为不同，当它们包含资源：它**不**允许索引到一个数组来读取在特定索引或分配给它，或索引的元素到一个字典来读取特定键的值或设定为一个值钥匙。

相反，使用 swap 语句 ( `<->`) 或 shift 语句 ( `<- target <-`) 将访问的资源替换为另一个资源。

```cadence
resource R {}

// Declare a constant for an array of resources.
// Create two resources and move them into the array.
// `resources` has type `@[R]`
//
let resources <- [
    <-create R(),
    <-create R()
]

// Invalid: Reading an element from a resource array is not allowed.
//
let firstResource <- resources[0]

// Invalid: Setting an element in a resource array is not allowed,
// as it would result in the loss of the current value.
//
resources[0] <- create R()

// Instead, when attempting to either read an element or update an element
// in a resource array, use a swap statement with a variable to replace
// the accessed element.
//
var res <- create R()
resources[0] <-> res
// `resources[0]` now contains the new resource.
// `res` now contains the old resource.

// Use the shift statement to move the new resource into
// the array at the same time that the old resource is being moved out
let oldRes <- resources[0] <- create R()
// The old object still needs to be handled
destroy oldRes
```

这同样适用于字典。

```cadence
// Declare a constant for a dictionary of resources.
// Create two resources and move them into the dictionary.
// `resources` has type `@{String: R}`
//
let resources <- {
    "r1": <-create R(),
    "r2": <-create R()
}

// Invalid: Reading an element from a resource dictionary is not allowed.
// It's not obvious that an access like this would have to remove
// the key from the dictionary.
//
let firstResource <- resources["r1"]

// Instead, make the removal explicit by using the `remove` function.
let firstResource <- resources.remove(key: "r1")

// Invalid: Setting an element in a resource dictionary is not allowed,
// as it would result in the loss of the current value.
//
resources["r1"] <- create R()

// Instead, when attempting to either read an element or update an element
// in a resource dictionary, use a swap statement with a variable to replace
// the accessed element.
//
var res <- create R()
resources["r1"] <-> res
// `resources["r1"]` now contains the new resource.
// `res` now contains the old resource.

// Use the shift statement to move the new resource into
// the dictionary at the same time that the old resource is being moved out
let oldRes <- resources["r2"] <- create R()
// The old object still needs to be handled
destroy oldRes
```

资源不能多次移动到数组和字典中，因为这会导致重复。

```cadence
let resource <- create R()

// Invalid: The resource variable `resource` can only be moved into the array once.
//
let resources <- [
    <-resource,
    <-resource
]
```

```cadence
let resource <- create R()

// Invalid: The resource variable `resource` can only be moved into the dictionary once.
let resources <- {
    "res1": <-resource,
    "res2": <-resource
}
```

资源数组和字典可以被销毁。

```cadence
let resources <- [
    <-create R(),
    <-create R()
]
destroy resources
```

```cadence
let resources <- {
    "r1": <-create R(),
    "r2": <-create R()
}
destroy resources
```

变量数组的功能类似于`append`, `insert`，并且其`remove` 行为类似于非资源数组。但是请注意，`remove`必须使用函数的结果。

```cadence
let resources <- [<-create R()]
// `resources.length` is `1`

resources.append(<-create R())
// `resources.length` is `2`

let first <- resource.remove(at: 0)
// `resources.length` is `1`
destroy first

resources.insert(at: 0, <-create R())
// `resources.length` is `2`

// Invalid: The statement ignores the result of the call to `remove`,
// which would result in a loss.
resource.remove(at: 0)

destroy resources
```

可变数组函数`contains`不可用，因为它是不可能的：如果资源可以传递给`contains`函数，则根据定义，它不在数组中。

变量数组功能`concat`不可用，因为它会导致资源重复。

字典的功能`insert`和`remove` 行为类似于非资源字典。但是请注意，必须使用这些函数的结果。

```cadence
let resources <- {"r1": <-create R()}
// `resources.length` is `1`

let first <- resource.remove(key: "r1")
// `resources.length` is `0`
destroy first

let old <- resources.insert(key: "r1", <-create R())
// `old` is nil, as there was no value for the key "r1"
// `resources.length` is `1`

let old2 <- resources.insert(key: "r1", <-create R())
// `old2` is the old value for the key "r1"
// `resources.length` is `1`

destroy old
destroy old2
destroy resources
```

### 资源标识符

资源有一个与之关联的隐式唯一标识符，由`let uuid: UInt64`每个资源上预先声明的公共字段实现。

这个标识符会在资源创建时自动设置，在资源的初始化程序被调用之前（即标识符可以在初始化程序中使用），并且即使在资源被销毁后也是唯一的，即没有两个资源会具有相同的标识符。

```cadence
// Declare a resource without any fields.
resource R {}

// Create two resources
let r1 <- create R()
let r2 <- create R()

// Get each resource's unique identifier
let id1 = r1.uuid
let id2 = r2.uuid

// Destroy the first resource
destroy r1

// Create a third resource
let r3 <- create R()

let id3 = r3.uuid

id1 != id2  // true
id2 != id3  // true
id3 != id1  // true
```

如何生成标识符的细节是一个实现细节。

不要依赖或假设 Cadence 程序中的任何特定行为。

## 未绑定引用/空值

有**没有**支持`null`。

## 继承和抽象类型

有**没有**支持继承。继承是其他编程语言中常见的特性，它允许将一种类型的字段和函数包含在另一种类型中。

相反，遵循“组合优于继承”的原则，即从多个单独的部分组合功能的想法，而不是构建继承树。

此外，**也不**支持抽象类型。抽象类型是其他编程语言中常见的特性，它阻止创建类型的值，只允许创建子类型的值。此外，抽象类型可以声明函数，但省略了它们的实现，而是需要子类型来实现它们。

相反，请考虑使用[接口](https://docs.onflow.org/cadence/language/interfaces)。