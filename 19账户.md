# 账户





每个帐户都可以通过两种类型访问：
1.作为 PublicAccount 类型的公共帐户，代表帐户的公开可用部分。

```
struct PublicAccount {

    let address: Address
    // 此账户默认金库的 FLOW 余额
    let balance: UFix64
    // 该账户默认的可移动的FLOW 金额
    let availableBalance: UFix64
    let storageUsed: UInt64
    let storageCapacity: UInt64

    // Keys
    let keys: PublicAccount.Keys

    // 存储操作

    fun getCapability<T>(_ path: PublicPath): Capability<T>
    fun getLinkTarget(_ path: CapabilityPath): Path?

    struct Keys {
        // 返回给定索引处的键（如果存在）。
		// 已撤销的键总是会返回，但它们的 `isRevoked` 字段设置为 true。
        fun get(keyIndex: Int): AccountKey?
    }
}
```

任何代码都可以使用内置的 getAccount 函数获取帐户地址的 PublicAccount：

```
fun getAccount(_ address: Address): PublicAccount
```

2.作为 AuthAccount 类型的授权帐户，代表帐户的授权部分。

访问 AuthAccount 意味着可以完全访问其存储、公钥和代码。
只有经过签名的交易才能获得账户的 AuthAccount。对于交易的每个脚本签名者，将相应的 AuthAccount 传递到交易的准备阶段。

```
struct AuthAccount {

    let address: Address
    // 此账户默认金库的 FLOW 余额
    let balance: UFix64
    // 该账户默认的可移动的FLOW 金额
    let availableBalance: UFix64
    let storageUsed: UInt64
    let storageCapacity: UInt64

    // 合同

    let contracts: AuthAccount.Contracts

    // 密钥

    let keys: AuthAccount.Keys

    // 密钥管理

    // 为账户添加一个公钥。
    // 公钥必须与其签名算法、散列算法和权重一起编码。
    // 此方法目前已弃用，仅用于向后兼容。
    // 可以使用`keys.add` 方法代替。
    fun addPublicKey(_ publicKey: [UInt8])

    // 撤销给定索引处的键。
    // 此方法目前已弃用，仅用于向后兼容。
    // 可以使用`keys.revoke` 方法代替。
    fun removePublicKey(_ index: Int)

    // 存储操作

    fun save<T>(_ value: T, to: StoragePath)
    fun load<T>(from: StoragePath): T?
    fun copy<T: AnyStruct>(from: StoragePath): T?

    fun borrow<T: &Any>(from: StoragePath): T?

    fun link<T: &Any>(_ newCapabilityPath: CapabilityPath, target: Path): Capability<T>?
    fun getCapability<T>(_ path: CapabilityPath): Capability<T>
    fun getLinkTarget(_ path: CapabilityPath): Path?
    fun unlink(_ path: CapabilityPath)

    struct Contracts {
        fun add(
            name: String,
            code: [UInt8],
            ... contractInitializerArguments
        ): DeployedContract

        fun update__experimental(name: String, code: [UInt8]): DeployedContract

        fun get(name: String): DeployedContract?

        fun remove(name: String): DeployedContract?
    }

    struct Keys {
        // 添加给定散列算法和权重的新密钥，并返回添加的密钥。
        fun add(
            publicKey: PublicKey,
            hashAlgorithm: HashAlgorithm,
            weight: UFix64
        ): AccountKey

       	// 返回给定索引处的键（如果存在），否则返回 nil。
   		// 撤销的键总是被返回，但它们的 `isRevoked` 字段设置为 true。
        fun get(keyIndex: Int): AccountKey?

        // 将给定索引处的键标记为已撤销，但不删除它。
		// 如果存在，则返回已撤销的密钥，否则返回 nil。
        fun revoke(keyIndex: Int): AccountKey?
    }
}

struct DeployedContract {
    let name: String
    let code: [UInt8]
}
```

## 帐户创建

可以通过调用 AuthAccount 构造函数并为 payer 参数传递应该为帐户创建付费的帐户来创建帐户。
付款人必须有足够的资金才能创建一个帐户。如果帐户没有所需的资金，程序将中止。

```
transaction() {
    prepare(signer: AuthAccount) {
        let account = AuthAccount(payer: signer)
    }
}
```

## 帐户密钥

一个帐户（PublicAccount 和 AuthAccount）都有与之关联的密钥。帐户密钥具有以下结构。

```
struct AccountKey {
    let keyIndex: Int
    let publicKey: PublicKey
    let hashAlgorithm: HashAlgorithm
    let weight: UFix64
    let isRevoked: Bool
}
```

有关公钥的创建和有效性的更多详细信息，请参阅公钥部分。

## 账户密钥 API

账户密钥 API 提供了一组管理账户密钥的函数。

### 添加帐户密钥

 要授权访问帐户，可以使用 add() 函数添加密钥。密钥只能添加到 AuthAccount。
例如，创建一个帐户并让交易的签名者为帐户创建付费，并授权一键访问该帐户：

```
transaction(publicKey: [UInt8]) {
    prepare(signer: AuthAccount) {
        let key = PublicKey(
            publicKey: publicKey,
            signatureAlgorithm: SignatureAlgorithm.ECDSA_P256
        )

        let account = AuthAccount(payer: signer)

        account.keys.add(
            publicKey: key,
            hashAlgorithm: HashAlgorithm.SHA3_256,
            weight: 10.0
        )
    }
}
```

⚠️ 注意：也可以使用 `addPublicKey` 函数添加密钥。但是，此方法目前已弃用，仅可用于向后兼容。 `addPublicKey` 方法接受与其签名算法、散列算法和权重一起编码的公钥。

```
transaction(key: [UInt8]) {
    prepare(signer: AuthAccount) {
        let account = AuthAccount(payer: signer)
        account.addPublicKey(key)
    }
}
```

### 获取帐户密钥

可以使用 get() 函数，使用键的索引来检索添加到帐户中的键。已撤销的键总是会返回，但它们的 isRevoked 字段设置为 true。如果给定索引处没有可用的键，则返回 nil。可以从 PublicAccout 和 AuthAccount 中检索密钥。

```
transaction() {
    prepare(signer: AuthAccount) {
        // Get a key from an auth account.
        let keyA = signer.keys.get(2)

        // Get a key from the public aacount.
        let publicAccount = getAccount(0x42)
        let keyB = publicAccount.keys.get(2)
    }
}
```

### 撤销帐户密钥

可以使用 revoke() 函数撤销已添加到帐户中的密钥。 Revoke 函数仅将给定索引处的键标记为已撤销，但不会删除它。只能从 AuthAccount 撤销密钥。

```
transaction() {
    prepare(signer: AuthAccount) {
        // Get a key from an auth account.
        let keyA = signer.keys.revoke(2)
    }
}
```

⚠️ 注意：也可以使用 `removePublicKey` 函数删除密钥。但是，此方法已弃用，仅可用于向后兼容。

## 账户存储

所有帐户都有存储空间。资源和结构都可以存储在帐户存储中。

## 路径

对象存储在路径下。路径由域和标识符组成。
路径以字符 / 开头，然后是域，路径分隔符 /，最后是标识符。例如，路径 /storage/test 具有域存储和标识符 test。
只有三个有效的域：存储、私有和公共。
存储中的对象始终存储在存储域中。
存储域中的路径类型为 StoragePath，在私有域 PrivatePath 和公共域 PublicPath 中。
PrivatePath 和 PublicPath 是 CapabilityPath 的子类型。
StoragePath 和 CapabilityPath 都是 Path 的子类型。

![image-20210707204405873](C:\Users\36927\AppData\Roaming\Typora\typora-user-images\image-20210707204405873.png)

## 账户存储API

账户存储通过AuthAccount的以下功能访问。这意味着任何有权访问授权帐户的代码都可以访问其所有存储的对象。

1.

```
fun save<T>(_ value: T, to: StoragePath)
```

将对象保存到帐户存储。资源被移动到存储中，结构被复制。
T 是对象类型的类型参数。它可以从参数的类型推断出来。
如果给定路径下已经存储了一个对象，则程序中止。
路径必须是存储路径，即只允许域存储。

2.

```
fun load<T>(from: StoragePath): T?
```

从帐户存储加载对象。如果没有对象存储在给定路径下，则函数返回 nil。如果存储了对象，则将存储的资源或结构移出存储并作为可选项返回。当函数返回时，存储中不再包含给定路径下的对象。

T 是对象类型的类型参数。必须显式提供参数的类型参数。
类型 T 必须是加载对象类型的超类型。如果不是，则函数返回 nil。给定的类型不一定需要与加载对象的类型完全相同。
路径必须是存储路径，即只允许域存储。

3.

```
fun copy<T: AnyStruct>(from: StoragePath): T?
```

返回存储在帐户存储中的结构的副本，而不将其从存储中删除。
如果给定路径下没有存储结构，则函数返回 nil。如果存储了结构，则将其复制。函数返回后，该结构仍存储在存储中。
T 是结构类型的类型参数。必须显式提供参数的类型参数。
类型 T 必须是复制结构类型的超类型。如果不是，则函数返回 nil。给定的类型不一定需要与复制结构的类型完全相同。
路径必须是存储路径，即只允许域存储。

```
// 声明一个名为“Counter”的资源。
//
resource Counter {
    pub var count: Int

    pub init(count: Int) {
        self.count = count
    }
}

// 在此示例中，授权帐户可通过常量 `authAccount` 获得。
// 创建资源类型 `Counter` 的新实例
// 并将其保存在帐户的存储中。
//
// 路径`/storage/counter` 用于引用存储的值。
// 它的标识符 `counter` 是自由选择的，可以是别的东西。
//
authAccount.save(<-create Counter(count: 42), to: /storage/counter)

// 运行时错误：存储已包含路径`/storage/counter` 下的对象
//
authAccount.save(<-create Counter(count: 123), to: /storage/counter)

// 从存储路径 `/storage/counter` 加载 `Counter` 资源。
//
// 新常量 `counter` 的类型为 `Counter?`，即它是一个可选的，
// 它的值是counter资源，在开始时保存示例。
//
let counter <- authAccount.load<@Counter>(from: /storage/counter)

// 在路径`/storage/counter` 下，存储现在是空的，不再有对象存储
// 从存储路径 `/storage/counter` 再次加载 `Counter` 资源。
//
// 新常量 `counter2` 的类型为 `Counter?` 并且是 `nil`，
// 因为在路径 `/storage/counter` 下不再存储任何东西，
// 因为之前的加载将counter移出存储空间。
//
let counter2 <- authAccount.load<@Counter>(from: /storage/counter)

// 创建资源类型 `Counter` 的另一个新实例
// 并将其保存在帐户的存储中。
//
// 路径`/storage/otherCounter` 用于引用存储的值。
//
authAccount.save(<-create Counter(count: 123), to: /storage/otherCounter)

// 从存储路径 `/storage/otherCounter` 加载 `Vault` 资源。
//
// 新常量 `vault` 的类型为 `Vault?`，其值为 `nil`，
// 因为路径下存储了一个类型为 `Counter` 的资源，
// 这不是所请求类型 `Vault` 的子类型。
//
let vault <- authAccount.load<@Vault>(from: /storage/otherCounter)

// storage 仍然在`/storage/otherCounter` 路径下存储了一个`Counter` 资源。
// 将字符串“Hello, World”保存在存储中
// 在路径 `/storage/helloWorldMessage` 下。

authAccount.save("Hello, world!", to: /storage/helloWorldMessage)

// 从存储中复制存储的消息。
//
// 复制后，storage 仍然存储路径下的字符串。
// 与 `load` 不同，`copy` 不会从存储中删除对象。
//
let message = authAccount.copy<String>(from: /storage/helloWorldMessage)

// 创建资源类型`Vault`的新实例
// 并将其保存在帐户的存储中。
//
authAccount.save(<-createEmptyVault(), to: /storage/vault)

// 无效：无法复制资源，因为这将允许任意复制。
//
let vault <- authAccount.copy<@Vault>(from: /storage/vault)
```

由于使用存储中的对象很方便，而不必将它们移出存储，因为这是资源所必需的，因此还可以创建对存储中对象的引用：这可以使用 AuthAccount 的借用功能：

4.

```cadence
fun borrow<T: &Any>(from: StoragePath): T?
```

返回对存储中对象的引用，而不将其从存储中删除。如果没有对象存储在给定路径下，则函数返回 nil。如果存储了一个对象，则返回一个引用作为可选。
T 是对象类型的类型参数。必须显式提供参数的类型参数。类型参数必须是对任何类型的引用（&Any; Any 是所有类型的超类型）。必须可以为存储/借用对象创建给定的引用类型 T。如果不是，则函数返回 nil。给定的类型不一定需要与借用对象的类型完全相同。
路径必须是存储路径，即只允许域存储。

```
// 申明一个资源接口 `HasCount`,它有一个只能读的资源`count`
//
resource interface HasCount {
    count: Int
}

// 申明一个资源接口 `Counter`,它有一可修改的资源`count`，并且继承`HasCount`
//
resource Counter: HasCount {
    pub var count: Int

    pub init(count: Int) {
        self.count = count
    }
}

// 在此示例中，授权帐户可通过常量 `authAccount` 获得。
// 创建资源类型 `Counter` 的新实例并将其保存在帐户的存储中。
//
// 路径`/storage/counter` 用于引用存储的值。
// 它的标识符 `counter` 是自由选择的，可以是别的东西。
//
authAccount.save(<-create Counter(count: 42), to: /storage/counter)

// 创建对存储在路径`/storage/counter` 下的对象的引用，
// 输入为 `&Counter`。
//
// `counterRef` 的类型为 `&Counter?` 并且是一个有效的引用，即非`nil`，
// 因为借用成功：
//
// 路径`/storage/counter` 下存储了一个对象
// 它的类型为 `Counter`，因此可以借用为 `&Counter`
//
let counterRef = authAccount.borrow<&Counter>(from: /storage/counter)

counterRef?.count // is `42`

// 创建对存储在路径`/storage/counter` 下的对象的引用，
// 输入为 `&{HasCount}`。
//
// `hasCountRef` 非 `nil`，因为在路径 `/storage/counter` 下存储了一个对象，
// 并且 `Counter` 类型的存储值符合请求的类型 `{HasCount}`：
// 类型 `Counter` 实现了受限类型的限制 `HasCount`

let hasCountRef = authAccount.borrow<&{HasCount}>(from: /storage/counter)

// 创建对存储在路径`/storage/counter` 下的对象的引用，
// 输入为 `&{SomethingElse}`。
//
// `otherRef` 为 `nil`，因为在路径 `/storage/counter` 下存储了一个对象，
// 但是 `Counter` 类型的存储值不符合请求的类型 `{Other}`：
// 类型 `Counter` 没有实现受限类型的限制 `Other`

let otherRef = authAccount.borrow<&{Other}>(from: /storage/counter)

// 创建对存储在路径`/storage/nonExistent` 下的对象的引用，
// 输入为 `&{HasCount}`。
//
// `nonExistentRef` 是 `nil`，因为在路径 `/storage/nonExistent` 下没有存储任何东西
let nonExistentRef = authAccount.borrow<&{HasCount}>(from: /storage/nonExistent)
```

## 存储限制

帐户存储受其存储容量的限制。
在每笔交易结束时，将使用的所有帐户存储与其存储容量进行比较。如果对于任何帐户，其使用的存储空间大于其存储容量，则交易失败。
可以使用 storageUsed 和 storageCapacity 字段检查帐户的已用存储和存储容量。这些字段表示存储的当前值，这意味着这是真的：

```
let storageUsedBefore = authAccount.storageUsed
authAccount.save(<-create Counter(count: 123), to: /storage/counter)
let storageUsedAfter = authAccount.storageUsed

let storageUsedChanged = storageUsedBefore != storageUsedAfter // is true
```

